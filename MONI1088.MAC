; ************************************************************
; *                                                          *
; *  MONITOR 10/88 fuer den AC 1     Version vom 29.08.2023  *
; *                                                          *
; ************************************************************
;
; Aenderungen:
;
; MONI88 - Korrekturen an der Originalversion 1988: 
; - Fuer die Nutzung der Grafiktaste wurde Adresse 02A9H 
;   von FFH auf 21H geaendert
; - korrekte V.24-Baudraten fuer den Betrieb mit 2 MHz
;   (das bisherige Timing war fuer 2,4768 MHz ausgelegt)
;   Korrekturen auf Adr. 00F9H bis 00FBH und 0E39H bis 0E3BH
;
; MONI88A - zus. Aenderungen an der Originalversion 1988:
; - Backspace/DEL mit 7FH (Adr. 0323H)
; - RAM loeschen mit Kommando "p" von 1858H bis 0FEFFH
; - die Monitorkommandos "X", "b" und "r" wurden entfernt
; - neues Monitorkommando "X": Start der AC1 ROM-Bank
; - PIO2 belegt die I/O-Adressen 18H bis 1BH
;
; MONI11 - Korrekturen an der BWS-CPLD-Version 2011:
; - Fehler beim Monitorkommando "7" fuer die PIO2-Karte:
;   Adresse 0FD4H von 10H auf 20H korrigiert
;
; MONI11A - zus. Aenderungen an der BWS-CPLD-Version 2011:
; - Farbbyte 0F9FH ist 4FH (= weiss auf blauem Hintergrund)
; - V.24-Kontrollregister (Adr. 023EH) wieder auf 02H
;
;
; ***************************************************
; *  Wichtig: Hier muss die gewuenschte             *
; *           Monitorversion auskommentiert werden  *
; ***************************************************
MONI88  EQU  1       ;Monitor 10/88 - Originalversion 1988
;MONI88A EQU  1       ;Monitor 10/88 - Originalversion mit Anpassungen
;MONI11  EQU  1       ;Monitor 10/88 - BWS-CPLD-Version 2011
;MONI11A EQU  1       ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
;
        PAGE 64
;
        IF1
        IFDEF MONI88
        .PRINTX 'Monitor 10/88 - Originalversion 1988'
        ENDIF
        IFDEF MONI88A
        .PRINTX 'Monitor 10/88 - Originalversion mit Anpassungen'
        ENDIF
        IFDEF MONI11
        .PRINTX 'Monitor 10/88 - BWS-CPLD-Version 2011'
        ENDIF
        IFDEF MONI11A
        .PRINTX 'Monitor 10/88 - BWS-CPLD-Version mit Anpassungen'
        ENDIF
        .PRINTX 'PASS 1'
        ENDIF
;
        IF2
        .PRINTX 'PASS 2'
        ENDIF
;
        .CREF
        .Z80
;
KURSOR  EQU  7FH          ;Kursorsymbol
;
KURPOS  EQU  1800H        ;Zwischenspeicher Kursor
ZWINL   EQU  181AH        ;Zwischenspeicher 'Inline'
COLOR   EQU  181FH        ;Merkzelle fuer das Farbbyte im RAM
V24REG  EQU  1820H        ;V.24-Kontrollregister
IOBYTE  EQU  1821H        ;Merkzelle fuer das I/O-Byte
TASCOD  EQU  1822H        ;Tastencode der zuletzt gedr端ckten Taste
HREGIN  EQU  1857H        ;Hilfsregister UP 'INLINE'
ARG1    EQU  185BH        ;Zwischenspeicher Argument 1
ARG2    EQU  185DH        ;Zwischenspeicher Argument 2
ARG3    EQU  185FH        ;Zwischenspeicher Argument 3
;
P00     EQU  00H          ;CTC Kanal 0
P01     EQU  01H          ;CTC Kanal 1
P02     EQU  02H          ;CTC Kanal 2
P03     EQU  03H          ;CTC Kanal 3
P04     EQU  04H          ;PIO 1 - Port A (Daten)
P05     EQU  05H          ;PIO 1 - Port B (Daten)
P06     EQU  06H          ;PIO 1 - Port A (Control)
P07     EQU  07H          ;PIO 1 - Port B (Control)
IFDEF MONI88A             ;MONI88A hat PIO2 I/O-Adr. 18H...1BH
P08     EQU  18H          ;PIO 2 - Port A (Daten)
P09     EQU  19H          ;PIO 2 - Port B (Daten)
P0A     EQU  1AH          ;PIO 2 - Port A (Control)
P0B     EQU  1BH          ;PIO 2 - Port B (Control)
ELSE                      ;Original: PIO2 I/O-Adr. 08H...0BH
P08     EQU  08H          ;PIO 2 - Port A (Daten)
P09     EQU  09H          ;PIO 2 - Port B (Daten)
P0A     EQU  0AH          ;PIO 2 - Port A (Control)
P0B     EQU  0BH          ;PIO 2 - Port B (Control)
ENDIF

P0E     EQU  0EH          ;ROM-Bank: EPROM-Einblendung
P0F     EQU  0FH          ;PIO2-Karte: EPROM-Einblendung
P14     EQU  14H          ;Modul 1: Konfigurationsregister
P16     EQU  16H          ;RAM 0000H bis 0FFFH einblenden
PF0     EQU  0F0H         ;COLOR-BWS
;
        .PHASE 0000H
;
L0000:  DI 
        LD   SP,1856H     ;Stack 
        IM   2 
        JR   INIT1 
;
;
; RST 8H - Eingabekanal, normal Tastatur
; --------------------------------------
;
L0008:  JP   1802H        ;Sprungtabelle 1802H
;
;
; INIT1 nach Reset
;
INIT1:  CALL MS30         ;Zeitschleife 30ms
        JR   INIT2 
;
;
; RST 10H - Ausgabekanal, normal Bildschirm
; -----------------------------------------
;
L0010:  JP   1805H        ;Sprungtabelle 1805H
;
;
; Einsprungpunkt fuer RST 10H   (Ausgabekanal, normal Bildschirm)
; ---------------------------
;
RST10:  PUSH HL 
        PUSH AF 
        JP   RST10A 
;
;
; RST 18H - Zeichenkettenausgabe bis incl. des Bytes, wo Bit 7 gesetzt ist
; ------------------------------------------------------------------------
;
L0018:  JP   1808H        ;Sprungtabelle 1808H
;
;
; Init2 nach Reset
;
INIT2:  LD   DE,KURPOS
        JR   INIT3 
;
;
; RST 20H - nicht verwendet
; -------------------------
;
L0020:  JP   180BH        ;Sprungtabelle 180BH
;
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        DEFS 5,0FFH       ;Platzhalter 5 Byte
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
        DEFS 5,0FFH       ;Platzhalter 5 Byte
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
;
; Codeschnipsel zur Funktion "8"
; ------------------------------
;
FKT8:   LD   A,40H        ;Codeschnipsel zur Funktion "8"
        JP   FKT0         ;A auf Port 0FH ausgeben
;
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
; Codeschnipsel zur Funktion "8"
; ------------------------------
;
FKT8:   LD   A,40H        ;Codeschnipsel zur Funktion "8"
        JP   FKT0         ;A auf Port 0FH ausgeben
;
        ENDIF
;
;
; RST 28H - nicht verwendet
; -------------------------
;
L0028:  JP   180EH        ;Sprungtabelle 180EH
;
;
; Funktion Ausgabe ueber Bildschirm - Teil 2
; ------------------------------------------
;
FKT33A: PUSH HL 
        PUSH DE 
        JP   FKT33B 
;
;
; RST 30H - nicht verwendet
; -------------------------
;
L0030:  JP   1811H        ;Sprungtabelle 1811H
;
;
; Funktion auf Adresse 0033H - Ausgabe ueber Bildschirm
; -----------------------------------------------------
;
FKT33:  PUSH AF 
        JR   FKT33A 
;
        DEFS 2,0FFH       ;Platzhalter 2 Byte
;
; RST 38H - Breakpoint
; --------------------
;
        JP   1814H        ;Sprungtabelle 1814H
;
;
; INIT3 nach Reset
;
INIT3:  LD   A,02H        ;HWT vom Interruptvektor
        LD   I,A 
        LD   HL,TABIO     ;Tabelle fuer IO-Befehle
        LD   B,11H        ;insgesamt 17 Befehle an versch. Ports
INIT4:  LD   C,(HL) 
        INC  HL 
        OUTI 
        JR   NZ,INIT4 
        EX   DE,HL 
        LD   B,60H        ;1800H bis 185FH mit 0FFH beschreiben
INIT5:  LD   (HL),0FFH    ;HL=1800H
        INC  HL 
        DJNZ INIT5 
        LD   BC,0020H     ;Laenge = 32 Bytes
        LD   HL,SPTAB     ;Sprungtabelle = 0220H
        LD   DE,1802H     ;Ziel = 1802H
        LDIR 
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        CALL UPTAST       ;Taste gedrueckt?
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
        CALL UPTAST       ;Taste gedrueckt?
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
        CALL FARBIN       ;CALL Farb-Initialisierung
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
        CALL FARBIN       ;CALL Farb-Initialisierung
        ENDIF
        JR   NZ,INIT6     ;Taste gedrueckt (nur Originalversion 1988)
        JR   INIT7 
;
        DEFS 2,0FFH       ;Platzhalter 2 Byte
;
; NMI-Befehl, Programmunterbrechung
; ---------------------------------
;
L0066:  JP   1817H        ;Sprungtabelle 1817H
;
;
INIT6:  CP   58H          ;Taste "X" gedrueckt?
JUMPX:  JP   Z,PROGX      ;Sprung zum Programmpaket X
INIT7:  XOR  A 
        OUT  (P14),A      ;Modul 1: Konfigurationsregister ruecksetzen
        RST  18H 
        DEFB 0CH,0DH,0FH,0FH 
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        DEFM "AC 1 * MONITOR *" 
        DEFM " U880  * (10/88)" 
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
        DEFM "AC 1 * MONITOR *" 
        DEFM " U880  * (10/88)" 
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
        DEFM "AC 1 * MONITOR (" 
        DEFM "10/88) * 11/2011" 
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
        DEFM "AC 1 * MONITOR (" 
        DEFM "10/88) * 11/2011" 
        ENDIF
        DEFB 0DH,0DH+80H 
        JP   GETCO        ;Sprung in die Monitoreingabeschleife
;
;
; Tasten-Piep
;
PIEP:   LD   (HL),C 
        LD   BC,0A034H
        CALL UPTON        ;UP 'Ton', Reg, B = Tonlaenge, C = Tonhoehe
ENDE8:  POP  BC 
        POP  HL 
        RET 
;
;
; Einsprungpunkt fuer RST 08H   (Eingabekanal, normal Tastatur)
; ---------------------------
;
RST08:  PUSH HL 
        PUSH BC 
        LD   A,(IOBYTE)   ;LD A,I/O-Byte 
        RRCA              ;Rotiere A-Register rechts
        JR   NC,NOTAST    ;anderes Eingabegeraet (keine Tastatur)
        LD   HL,HREGIN    ;Hilfsregister UP 'INLINE'
        LD   B,(HL) 
        LD   (HL),18H 
RST08A: CALL UPTAST       ;Taste gedrueckt?
        JR   Z,RST08B     ;nein
        DEC  B            ;ja
        JR   NZ,RST08A    ;Tasten-Entprellung
        LD   (HL),01H 
RST08B: LD   HL,(KURPOS) 
        LD   C,(HL)       ;bisheriges BS-Zeichen in C merken
RST08C: LD   (HL),KURSOR  ;Kursorsymbol = 7FH setzen
RST08D: CALL UPTAST       ;Taste gedrueckt?
        JR   NZ,PIEP      ;ja --> Tasten-Piep --> und RET
        LD   A,48H 
RST08E: DEC  A 
        JR   NZ,RST08E    ;Zeitschleife A
        DJNZ RST08D       ;Zeitschleife B
        LD   A,C          ;bisheriges BS-Zeichen nach A
        CP   (HL) 
        JR   Z,RST08C     ;Kursor setzen
        LD   (HL),C 
        JR   RST08D       ;bisheriges BS-Zeichen setzen
;
NOTAST: RRCA              ;Rotiere A-Register rechts
        JR   NC,NOV24     ;anderes Eingabegeraet (kein V.24)
;
;
; Eingabegeraet = V.24
; --------------------
;
V24IN1: LD   A,(V24REG)   ;V.24-Kontrollregister
        AND  07H          ;Selektion der Baudrate
        LD   B,A          ;B = Baudrate (kodiert)
        LD   A,01H 
V24IN2: ADD  A,A 
        DJNZ V24IN2 
        LD   H,A          ;19200 Baud --> H = 2
        XOR  A 
        OUT  (P08),A      ;Steuerleitung auf Low setzen
V24IN3: IN   A,(P08)      ;Daten lesen PIO2, Port A
        AND  01H          ;BIT 0 auswerten (RxD)
        JR   NZ,V24IN3    ;warten auf fallende Flanke
        LD   B,H
V24IN4: LD   A,R          ;Zeitschleife bis zur Bit-Abtastung
        LD   A,A
        DJNZ V24IN4
        LD   B,10         ;10 Durchlaeufe Bit-Abtastung
V24IN5: LD   L,H 
        RR   C            ;C rechts rotieren durch CY
V24IN6: LD   L,(HL)       ;Baudratenanpassung fuer 2 MHz
        INC  IX           ;Baudratenanpassung fuer 2 MHz
        DEC  L 
        IN   A,(P08)      ;Daten lesen PIO2, Port A
        JR   NZ,V24IN6    ;warten auf fallende Flanke
        RRA               ;empfangenes Bit 0 --> CY
        DJNZ V24IN5 
        LD   A,0AH
        OUT  (P08),A      ;Steuerleitung auf High setzen
        LD   A,C          ;empfangenes Zeichen --> A
        JR   ENDE8        ;Ende von RST08
;
        DEFB 0FFH         ;Platzhalter 1 Byte
;
; anderes Eingabegeraet (keine Tastatur, kein V.24)
;
NOV24:  RRCA              ;Rotiere A-Register rechts
        CALL C,0FFFFH     ;Reserve --> nicht belegt --> Fehlermeldung
        RRCA              ;Rotiere A-Register rechts
        CALL C,18F0H      ;Sprung zum UP User-Eingabe
        JR   ENDE8        ;Ende von RST08
;
;
; Start vom Programmpaket X
; -------------------------
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
PROGX:  LD   A,01H 
        OUT  (P14),A      ;Modul 1: EPROM mit Programmpaket X
        JP   0E000H       ;Sprung zum Programmpaket X
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
PROGX:  JP   PROGX1       ;Sprung zur AC1 ROM-Bank
        DEFS 4,0FFH       ;Platzhalter 4 Byte
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
PROGX:  LD   A,01H 
        OUT  (P14),A      ;Modul 1: EPROM mit Programmpaket X
        JP   0E000H       ;Sprung zum Programmpaket X
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
PROGX:  LD   A,01H 
        OUT  (P14),A      ;Modul 1: EPROM mit Programmpaket X
        JP   0E000H       ;Sprung zum Programmpaket X
        ENDIF
;
;
; UP 'MS 30', Zeitschleife 30 ms
; ------------------------------
;
; 26 Takte * 2307 Durchlaeufe = 59982 Takte --> 30 ms
;
MS30:   PUSH BC 
        LD   BC,0903H     ;Zeitkonstante fuer 30ms
MS30A:  DEC  BC           ;6 Takte
        LD   A,B          ;4 Takte
        OR   C            ;4 Takte
        JR   NZ,MS30A     ;12 Takte 
        POP  BC 
        RET 
;
;
; INLINE:
; liest eine Zeile, die mit CR abgeschlossen wird, auf dem Schirm
; ein und legt die RAM-Adresse vom Anfang der Zeile in den Zellen 
; 181A und 181B ab
; ---------------------------------------------------------------
;
INLINE: PUSH AF 
        PUSH HL 
        RST  18H 
        DEFM " #"         ;Prompt ausgeben
        DEFB 20H+80H 
        NOP 
INLIN1: RST  08H          ;1 Zeichen lesen
        LD   HL,(KURPOS)  ;Kursorposition merken
        RST  10H          ;1 Zeichen ausgeben
        CP   0DH 
        JR   NZ,INLIN1    ;Wiederholung, bis CR gedrueckt wurde
        LD   A,23H        ;"#"
INLIN2: INC  HL           ;1 Kursorposition zurueck
        CP   (HL)         ;sind wir beim Prompt angekommen?
        JR   NZ,INLIN2    ;nein
        DEC  HL 
        DEC  HL           ;jetzt 2 Kursorpositionen nach vorn
        LD   (ZWINL),HL   ;Bildschirm-Adresse speichern
        POP  HL 
        POP  AF 
        RET 
;
;
; UP-Routine zu INHEX
; -------------------
;
UPINHX: LD   A,(DE) 
        CP   20H          ;Leerzeichen am Anfang ignorieren
        DEC  DE 
        JR   Z,UPINHX 
        INC  DE 
        XOR  A 
        LD   HL,1858H     ;Zwischenspeicher initialisieren
        LD   (HL),A 
        INC  HL 
        LD   (HL),A 
        INC  HL 
        LD   (HL),A 
UPINH1: LD   A,(DE)       ;ASCII-Zahl lesen
        DEC  HL 
        DEC  HL           ;HL = 1858H
        SUB  30H          ;ASCII --> BIN
        RET  M            ;...wenn Ergebnis negativ ist
        CP   0AH          ;A > "9" ?
        JR   C,UPINH2     ;nein
        SUB  07H          ;41H - 30H - 07H = 0AH Bingo. 
        CP   0AH          ;A < "A" ?
        RET  M 
        CP   10H          ;A > "F" ?
        RET  P 
UPINH2: DEC  DE           ;DE --> naechste Ziffer
        INC  (HL)         ;(1858H) += 1
        INC  HL 
        RLD               ;Rotiere Ziffer links zw. A und (HL)
        INC  HL 
        RLD               ;Rotiere Ziffer links zw. A und (HL)
        JR   UPINH1 
;
;
; UP INHEX
; --------
;
;Wandelt eine maximal vierstellige in ASCII-Zeichen angegebene
;Zahl ab (DE) abwaerts in deren hexadezimalen Wert um, der dann
;in HL steht. DE wird entsprechend dekrementiert.
;
INHEX:  PUSH BC 
        PUSH AF 
        CALL UPINHX       ;Umwandlung ASCII --> HEX
        INC  HL           ;HL = 1859H
        LD   B,H 
        LD   C,L          ;BC = HL
        LD   L,(HL)       ;NWT --> L
        INC  BC
        LD   A,(BC)
        LD   H,A          ;HWT --> H
        POP  BC           ;AF zurueckholen nach BC
        OR   L            ;Flags setzen (HL = 0?)
        LD   A,B          ;Akku auf alten Wert setzen
        POP  BC           ;BC zurueckholen
        RET 
;
;
; UP OUTHEX
; gibt den Akku als zweistellige Hexzahl auf dem Schirm aus
; ---------------------------------------------------------
;
OUTHEX: PUSH AF 
        RRA               ;Rechtsrotation von A durch CY
        RRA 
        RRA 
        RRA 
        CALL OUTHX1       ;HWT ausgeben
        POP  AF 
OUTHX1: PUSH AF           ;im 2. Durchlauf NWT ausgeben
        AND  0FH 
        ADD  A,30H 
        CP   3AH          ;A = (0..9)?
        JR   C,OUTHX2 
        ADD  A,07H        ;A = (A..F)
OUTHX2: RST  10H 
        POP  AF 
        RET 
;
;
; UP OUTHL
; gibt das HL-Register als vierstellige Hexzahl auf dem Schirm aus
; ----------------------------------------------------------------
;
OUTHL:  PUSH AF 
        LD   A,H 
        CALL OUTHEX       ;H ausgeben
        LD   A,L 
        CALL OUTHEX       ;L ausgeben
        POP  AF 
        RET 
;
;
; UP, CPU-Register ausraeumen nach RSA
; ------------------------------------
;
CPURSA: LD   (1858H),SP   ;Stack merken
        LD   SP,1875H     ;Stack auf das Ende der RSA setzen
        PUSH IY 
        PUSH IX 
        PUSH HL 
        PUSH DE 
        PUSH BC 
        PUSH AF 
        EXX 
        EX   AF,AF' 
        PUSH HL 
        PUSH DE 
        PUSH BC 
        PUSH AF 
        JR   RSACP1       ;alten Stack zurueck und Tschuess
;
;
; UP, CPU-Register einraeumen von RSA
; -----------------------------------
;
RSACPU: LD   (1858H),SP   ;Stack merken
        LD   SP,1861H     ;Stack auf den Anfang der RSA setzen
        POP  AF 
        POP  BC 
        POP  DE 
        POP  HL 
        EXX 
        EX   AF,AF' 
        POP  AF 
        POP  BC 
        POP  DE 
        POP  HL 
        POP  IX 
        POP  IY 
RSACP1: LD   SP,(1858H)   ;alten Stack zurueck
        RET 
;
;
; V.24 - Eingabe UP
; -----------------
;
V24IN:  PUSH HL
        PUSH BC 
        JP   V24IN1 
;
;
; kein gueltiges Zeichen bei der Kommandozeicheneingabe
; -----------------------------------------------------
;
KOMM0:  RST  18H 
        DEFM " " 
        DEFB 3FH+80H      ;"?+80H 
;
;
; Warteschleife Kommandozeicheneingabe
; ------------------------------------
;
KOMM1:  CALL INLINE       ;Kommandozeile lesen (Adresse 01DEH)
        LD   HL,0200H     ;Anfangsadresse fuer Suchbereich
        LD   BC,0FE00H    ;Laenge des Suchbereiches
KOMM2:  XOR  A            ;1. Such-Byte = 00H
        CPIR 
        JP   PO,KOMM0     ;BC=0 --> nichts gefunden, nochmal
        LD   A,09H        ;2. Such-Byte = 09H
        CP   (HL) 
        JR   NZ,KOMM2 
        INC  HL 
        LD   DE,(ZWINL)   ;3. Such-Byte = aus Kommandozeile
        LD   A,(DE) 
        CP   (HL) 
        JR   NZ,KOMM2 
        INC  HL 
        LD   A,0DH        ;4. Such-Byte = 0DH
        CP   (HL) 
        JR   NZ,KOMM2 
        LD   BC,01DEH     ;Adresse Kommandozeicheneingabe
        PUSH BC           ;als Ruecksprung bei RET+RET
        INC  HL           ;Startadresse vom gefundenen Kommando
        PUSH HL           ;als Ruecksprung bei RET
        DEC  DE           ;naechstes Zeichen in der Kommandozeile
        CALL INHEX 
        LD   A,(DE) 
        CP   3AH          ;":" --> gespeicherte Argumente nehmen
        JR   Z,KOMM3 
        LD   (ARG1),HL    ;Argumente aus der Kommandozeile laden
        CALL INHEX 
        LD   (ARG2),HL 
        CALL INHEX 
        LD   (ARG3),HL 
KOMM3:  RET
;
;
; Sprungverteiler fuer RST und NMI (insgesamt 32 Bytes)
; --------------------------------
;
; wird beim Systemstart nach Adresse 1802H kopiert
; 
SPTAB:  JP   RST08        ;Einsprungpunkt fuer RST 08H
        JP   RST10        ;Einsprungpunkt fuer RST 10H 
        JP   RST18        ;Einsprungpunkt fuer RST 18H
        JP   0FFFFH       ;Einsprungpunkt fuer RST 20H 
        JP   0FFFFH       ;Einsprungpunkt fuer RST 28H 
        JP   0FFFFH       ;Einsprungpunkt fuer RST 30H 
        JP   BREAK        ;Einsprungpunkt fuer RST 38H 
        JP   BREAK        ;Einsprungpunkt fuer NMI 
        DEFB 0FFH,0FFH    ;Zwischenspeicher 'Inline'
        DEFM "SCH"        ;Warmstartcode
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        DEFB 0AFH         ;Warmstartcode
        DEFB 02H          ;Kommando-Code V.24 (9600 Baud)
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
        DEFB 0AFH         ;Warmstartcode
        DEFB 02H          ;Kommando-Code V.24 (9600 Baud)
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
        DEFB 00H          ;Warmstartcode
        DEFB 43H          ;Kommando-Code V.24 (4800 Baud)
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
        DEFB 00H          ;Warmstartcode
        DEFB 02H          ;Kommando-Code V.24 (9600 Baud)
        ENDIF
        DEFB 11H          ;I/O-Byte (Adresse 1821H)
;
;
; Einsprungpunkt fuer RST 38H und NMI
; -----------------------------------
;
BREAK:  CALL CPURSA        ;UP, CPU-Register ausraeumen nach RSA
        POP  HL
        DEC  HL
        LD   (1875H),HL 
        LD   (1877H),SP 
        LD   SP,1856H     ;Stack 
        LD   A,11H 
        LD   (IOBYTE),A   ;LD I/O-Byte,A
        RST  18H 
        DEFM " BREA" 
        DEFB 4BH+80H      ;"K+80H 
;
;
; Monitoreingabeschleife, der Monitorstack wird neu initialisiert
; ----------------------
;
GETCO:  LD   SP,1856H     ;Stack 
        LD   A,0DAH
        OUT  (P05),A 
        LD   HL,01DEH     ;Adresse Kommandozeicheneingabe
        PUSH HL 
        RETN 
;
;
; UP 'ERROR', schreibt Zeichenkette 'ERROR'
; -----------------------------------------
;
ERROR:  RST  18H 
        DEFM "  ERROR" 
        DEFB 20H+80H 
        RET 
;
;
; UP 'akustisches Signal'
; -----------------------
;
TUELUE: PUSH BC 
        LD   B,02H 
TUELU1: PUSH BC 
        LD   BC,0040H 
        CALL UPTON        ;UP 'Ton', Reg, B = Tonlaenge, C = Tonhoehe
        LD   BC,0F032H
        CALL UPTON        ;UP 'Ton', Reg, B = Tonlaenge, C = Tonhoehe
        POP  BC 
        DJNZ TUELU1 
        POP  BC 
        RET 
;
;
; UP 'Ton', Reg, B = Tonlaenge, C = Tonhoehe
; ------------------------------------------
;
UPTON:  PUSH AF 
UPTON1: IN   A,(P05) 
        RRA               ;A0 --> CY
        CCF               ;CY = !CY
        RLA               ;CY --> A0
        OUT  (P05),A 
        LD   A,C 
UPTON2: DEC  A 
        JR   NZ,UPTON2 
        DJNZ UPTON1 
        POP  AF 
        RET 
;
;
; UP 'Taste', testet Tastaturstatus
; ---------------------------------
; kehrt bei gedr端ckter Taste nach 18 ms mit dem Code zum Akku zur端ck, 
; keine Taste --> R端ckkehr mit gesetzten Z-Flag und A=0
; Code steht auch in 1822H
;
UPTAST: PUSH BC 
        CALL UPTAS4       ;Tastatur abfragen
        JR   Z,UPTAS3     ;Taste nicht gedrueckt
        LD   B,0CH 
        LD   C,A          ;Tastencode in C
UPTAS1: CALL UPTAS4       ;Entprellung
        CP   C 
        JR   NZ,UPTAS3    ;Taste nicht (mehr) gedrueckt
        DJNZ UPTAS1 
;
; Einbau der Grafiktaste (nur Originaltastatur)                   
; ----------------------                                                                
; Der einpolige rastende Schalter kommt an PIO 1/B2 und Masse.    
; Von PIO 1/B2 einen R = 330 Ohm und LED VQA 13 nach 5P an-         
; schliessen. Die LED leuchtet bei eingeschalteter Grafiktastatur.                          
; Adresse 02A9 Datenbyte FF in 21 aendern. 
;                        
        CP   21H          ;21H --> Grafiktaste wird verwendet
        JR   C,UPTAS2     ;keine Grafik, wenn Tastencode < 21H ist
        IN   A,(P05)      ;PIO1 Port B lesen
        BIT  2,A          ;Bit 2 = Grafiktaste
        LD   A,C          ;Tastencode nach A
        JR   NZ,UPTAS2    ;Grafiktaste nicht aktiviert
        ADD  A,80H        ;7. Bit setzen
        CP   0E0H
        JR   C,UPTAS2     ;Grafik, wenn Tastencode < 0E0H ist
        SUB  60H          ;sonst Grafik-Wertebereich = 80H...9FH
UPTAS2: INC  B 
        DEFB 06H          ;LD  B,0AFH (incl. naechsten Befehl!!!)
UPTAS3: XOR  A            ;Einsprungpunkt "Taste nicht gedrueckt"
        POP  BC 
        LD   (TASCOD),A   ;Tastencode der zuletzt gedrueckten Taste
        RET 
;
UPTAS4: LD   A,48H        ;Zeitschleife
UPTAS5: DEC  A 
        JR   NZ,UPTAS5 
        IN   A,(P04)      ;PIO1 Port A lesen
        BIT  7,A          ;Taste gedrueckt?
        RES  7,A          ;7. Bit ruecksetzen
        RET 
;
;
; Funktion "W" --> Port ausgeben
;
        DEFB 00H,09H,57H,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC 
        LD   B,H          ;HL-->BC
        LD   C,L
        OUT  (C),E        ;OUT (C),r legt das komplette BC-Register
        RET               ;auf den Adressbus
;
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        DEFS 21,0FFH      ;Platzhalter 21 Byte
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
        DEFS 21,0FFH      ;Platzhalter 21 Byte
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
;
; Codeschnipsel fuer Farb-Initialisierung
;
FARBIN: CALL FINIT        ;Farb-Initialisierung 
        JR   UPTAST       ;Taste gedrueckt?
;
;
; Funktion "6" --> PIO2-Karte: Bank 1 einschalten
;
        DEFB 00H,09H,36H,0DH 
        LD   A,10H 
        JR   FKT0         ;A auf Port 0FH ausgeben
;
;
; Funktion "0" --> PIO2-Karte: EPROM ausblenden
;
        DEFB 00H,09H,30H,0DH 
        XOR  A
FKT0:   OUT  (P0F),A 
        RET 
;
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
; Codeschnipsel fuer Farb-Initialisierung
;
FARBIN: CALL FINIT        ;Farb-Initialisierung 
        JR   UPTAST       ;Taste gedrueckt?
;
;
; Funktion "6" --> PIO2-Karte: Bank 1 einschalten
;
        DEFB 00H,09H,36H,0DH 
        LD   A,10H 
        JR   FKT0         ;A auf Port 0FH ausgeben
;
;
; Funktion "0" --> PIO2-Karte: EPROM ausblenden
;
        DEFB 00H,09H,30H,0DH 
        XOR  A
FKT0:   OUT  (P0F),A 
        RET 
;
        ENDIF
;
;
; Zeichensatz SCCH <--> ACC umschalten
;
ZGUMS:  IN   A,(P05)
        XOR  08H 
        OUT  (P05),A 
        JR   BSEND3 
;
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        DEFB 5BH          ;Interruptvektor CTC Kanal 0 --> GETCO
        DEFB 07H          ;Interruptvektor CTC Kanal 1 --> nicht genutzt
        DEFS 6,0FFH       ;Platzhalter 6 Byte
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
        DEFB 5BH          ;Interruptvektor CTC Kanal 0 --> GETCO
        DEFB 07H          ;Interruptvektor CTC Kanal 1 --> nicht genutzt
        DEFS 6,0FFH       ;Platzhalter 6 Byte
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
;
        DEFB 5BH          ;Interruptvektor CTC Kanal 0 --> GETCO
        DEFB 5BH          ;Interruptvektor CTC Kanal 1 --> GETCO
;
;
; RAM von 0000H bis 0FFFH einschalten
;
ALTMO:  LD   (0FFFEH),HL  ;wird von Fkt. "Y" angesprungen
        JP   0FFFEH       ;Start alternativer Monitor im RAM
;
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
        DEFB 5BH          ;Interruptvektor CTC Kanal 0 --> GETCO
        DEFB 5BH          ;Interruptvektor CTC Kanal 1 --> GETCO
;
;
; RAM von 0000H bis 0FFFH einschalten
;
ALTMO:  LD   (0FFFEH),HL  ;wird von Fkt. "Y" angesprungen
        JP   0FFFEH       ;Start alternativer Monitor im RAM 
;
        ENDIF
;
;
; Funktion Ausgabe ueber Bildschirm - Teil 3
; ------------------------------------------
;
FKT33B: PUSH BC 
        LD   DE,(KURPOS)  ;Kursorposition auf dem BS
        LD   HL,1856H     ;Stack
        CP   10H          ;ist es ein BS-Steuerzeichen 00H...0FH?
        JR   NC,FKT33C    ;nein
        LD   (HL),01H     ;ja --> Merker in 1856H setzen
        LD   BC,03C7H     ;Adresse vom Sprungverteiler fuer BS-Befehle
        ADD  A,C          ;Wertebereich von A = 00H...0FH
        LD   C,A 
        LD   A,(BC)       ;NWT der Einsprungadresse holen, HWT = 03H
        LD   C,A 
        PUSH BC           ;Einsprungadresse in den Stack
        EX   DE,HL        ;Kursorposition in HL, Stack in DE
        RET               ;zur Einsprungadresse springen
;
FKT33C: DEC  (HL)         ;muessen noch Argumente uebergeben werden?
        JR   NZ,KUPOSI    ;ja (fuer Kursorpositionierung mit Kommando 0EH)
FKT33D: LD   (HL),01H     ;nein
        EX   DE,HL        ;Kursorposition in HL, Stack in DE
        CP   1AH          ;^Z = Zeichensatz umschalten
        JR   Z,ZGUMS 
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        CP   5FH          ;Backspace/DEL mit 5FH
        ELSE              ;alle anderen Monitor-Versionen
        CP   7FH          ;Backspace/DEL mit 7FH
        ENDIF
        JR   Z,DELETE 
        LD   (HL),A       ;lade Zeichen auf Kursorposition
;
; 09H --> Kursor nach rechts
;
BS0327: DEC  HL           ;Kursorposition--
BSEND1: CALL SCROLL       ;muss der BS gescrollt werden?
BSEND2: LD   (KURPOS),HL  ;Kursorposition in Kursorspeicher
BSEND3: POP  BC 
        POP  DE 
        POP  HL 
        POP  AF 
        RET 
;
; Auswertung der Argumente fuer die Kursorpositionierung
; 1. Argument = BS-Zeile - Zehner
; 2. Argument = BS-Zeile - Einer
; 3. Argument = BS-Spalte - Zehner
; 4. Argument = BS-Spalte - Einer
;
KUPOSI: CP   30H          ;A < "0"?   A = "0"?
        JR   C,FKT33D     ;kein gueltiges Argument
        JR   Z,BSEND3     ;bei "0" Funktion verlassen
        CP   3AH          ;A >= "9"?
        JR   NC,FKT33D    ;kein gueltiges Argument
        SUB  30H          ;A = 01H...09H
        LD   B,(HL)       ;lfd. Nr. des Arguments 1...4
        EX   DE,HL        ;Kursorposition in HL
        LD   DE,0FFFFH    ;--> 1 Zeichen nach rechts
        DEC  B 
        JR   Z,KUPOS1     ;4. Argument
        LD   E,0F6H       ;--> 10 Zeichen nach rechts
        DEC  B 
        JR   Z,KUPOS1     ;3. Argument
        LD   E,0C0H       ;--> 1 Zeile nach unten
        DEC  B 
        JR   Z,KUPOS1     ;2. Argument 
        LD   DE,0FD80H    ;--> 10 Zeilen nach unten
KUPOS1: LD   B,A 
KUPOS2: ADD  HL,DE        ;Kursorposition veraendern
        CALL SCROLL 
        DJNZ KUPOS2       ;1...9 mal wiederholen
        JR   BSEND2 
;
; Kursor nach links und Zeichen loeschen
;
DELETE: INC  HL           ;Kursorposition++
        CALL SCROLL 
        LD   (HL),20H 
        JR   BSEND2 
;
; 0CH --> Kursor Home + Bildschirm loeschen
;
BS0365: LD   HL,17FFH 
        LD   (KURPOS),HL  ;Kursor Home
;
; 02H --> Bildschirm ab Kursor loeschen
;
BS036B: LD   (HL),20H     ;Bildschirm loeschen
        DEC  HL 
        LD   A,H 
        CP   0FH 
        JR   NZ,BS036B 
        JR   BSEND3 
;
; 03H --> Zeile ab Kursorposition loeschen
;
BS0375: LD   (HL),20H 
CLRLIN: DEC  HL 
        LD   (HL),20H 
        LD   A,3FH 
        AND  L 
        JR   NZ,CLRLIN 
CLREND: JR   BSEND3 
;
; 06H --> Kursor an den Anfang der Zeile
;
BS0381: LD   A,L 
        OR   3FH 
        LD   L,A 
        JR   BSEND2 
;
; 07H --> akustisches Signal
;
BS0387: CALL TUELUE 
        JR   BSEND3 
;
; 08H --> Kursor nach links
;
BS038C: INC  HL 
        JR   BSEND1 
;
; 0AH --> Kursor nach unten
;
BS038F: LD   DE,0FFC0H
KURMOV: ADD  HL,DE 
        JR   BSEND1 
;
; 0BH --> Kursor nach oben
;
BS0395: LD   DE,0040H 
        JR   KURMOV 
;
; 0DH --> CR; Kursor an den Anfang naechster Zeile
;
BS039A: LD   A,L 
        AND  0C0H
        LD   L,A 
        JR   BS0327 
;
; 0EH --> Kursor direkt positionieren
;
BS03A0: LD   A,05H        ;4 Argumente folgen (2x Zeile, 2x Spalte)
        LD   (DE),A 
;
; 01H --> Home: Kursor nach oben links
;
BS03A3: LD   HL,17FFH 
        JR   BSEND2 
;
; 04H --> Delete: 1 Zeichen loeschen, Zeile rueckt nach links
;
BS03A8: LD   D,H 
        LD   E,L 
        DEC  HL 
DELE1:  LD   A,L 
        LDD 
        AND  3FH 
        JR   NZ,DELE1 
        INC  DE 
DELE2:  LD   A,20H 
        LD   (DE),A 
        JR   CLREND 
;
; 05H --> Insert: Space einfuegen, Zeile rueckt nach rechts
;
BS03B8: LD   A,L 
        AND  0C0H
        LD   E,A 
        LD   A,L 
        LD   L,E 
        LD   D,H 
        INC  HL 
INSE1:  CP   E 
        JR   Z,DELE2 
        LDI 
        JR   INSE1 
;
; Sprungverteiler (NWT) fuer BS-Befehle: (HWT = 03H)
;
BS03C7: DEFB 2EH          ;00H --> Fkt. verlassen (BSEND3)
        DEFB 0A3H         ;01H --> Home: Kursor nach oben links (BS03A3)
        DEFB 6BH          ;02H --> Bildschirm ab Kursor loeschen (BS036B)
        DEFB 75H          ;03H --> Zeile ab Kursorposition loeschen (BS0375)
        DEFB 0A8H         ;04H --> Delete: 1 Zeichen loeschen, Zeile rueckt nach links (BS03A8)
        DEFB 0B8H         ;05H --> Insert: Space einfuegen, Zeile rueckt nach rechts (BS03B8)
        DEFB 81H          ;06H --> Kursor an den Anfang der Zeile (BS0381)
        DEFB 87H          ;07H --> akustisches Signal (BS0387)
        DEFB 8CH          ;08H --> Kursor nach links (BS038C)
        DEFB 27H          ;09H --> Kursor nach rechts (BS0327)
        DEFB 8FH          ;0AH --> Kursor nach unten (BS038F)
        DEFB 95H          ;0BH --> Kursor nach oben (BS0395)
        DEFB 65H          ;0CH --> Kursor Home, Bildschirm loeschen (BS0365)
        DEFB 9AH          ;0DH --> CR; Kursor an den Anfang naechster Zeile (BS039A)
        DEFB 0A0H         ;0EH --> Kursor direkt positionieren (BS03A0)
        DEFB 0D7H         ;0FH --> Tabulator 8 Spalten (BS03D7)
;
; 0FH --> Tabulator 8 Spalten
;
BS03D7: LD   DE,0FFF8H
        JR   KURMOV 
;
; Bildschirm um 1 Zeile nach oben rollen
;
SCROLL: LD   A,H          ;HWT Kursorposition
        CP   18H          ;Kursor hat BS nach oben verlassen?
        JR   C,SCROL1     ;nein
        LD   H,10H        ;ja --> auf unterste BS-Zeile setzen
SCROL1: CP   10H          ;HWT im Bereich 10H...17H?
        RET  NC           ;ja --> kein Scrollen erforderlich
        LD   A,L          ;NWT Kursorposition merken
        PUSH DE 
        PUSH BC 
        LD   HL,17BFH 
        LD   DE,17FFH 
        LD   BC,07C0H 
        LDDR              ;1 Zeile nach oben scrollen
        EX   DE,HL 
        INC  HL 
SCROL2: DEC  L 
        LD   (HL),20H     ;unterste Zeile loeschen
        JR   NZ,SCROL2 
        AND  3FH 
        LD   L,A          ;Kursorposition NWT zurueck in L
        POP  BC 
        POP  DE 
        RET 
;
;
; Funktion "N" --> CRC - Pruefsumme (TODO)
;
        DEFB 00H,09H,4EH 
L0404:  DEC  C            ;= 0DH --> Der Sprung geht in die Kennung hinein
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC 
        EX   DE,HL 
        INC  HL 
        XOR  A 
        SBC  HL,DE 
        LD   BC,0FFFFH
L0410:  LD   A,(DE) 
        XOR  B 
        LD   B,A 
        RRCA 
        RRCA 
        RRCA 
        RRCA 
        AND  0FH 
        XOR  B 
        LD   B,A 
        RRCA 
        RRCA 
        RRCA 
        PUSH AF 
        AND  1FH 
        XOR  C 
        LD   C,A 
        POP  AF 
        PUSH AF 
        RRCA 
        AND  0F0H
        XOR  C 
        LD   C,A 
        POP  AF 
        AND  0E0H
        XOR  B 
        LD   B,C 
        LD   C,A 
        INC  DE 
        DEC  HL 
        LD   A,H 
        OR   L 
        JR   NZ,L0410 
        PUSH BC 
        RST  18H 
        DEFM " CRC" 
        DEFB 20H+80H 
        POP  HL 
        JP   OUTHL 
;
;
; ??? (TODO)
;
L0441:  LD   B,C 
        INC  HL 
L0443:  DEC  HL 
        DJNZ L0443 
        LD   (ARG2),HL 
        RST  18H 
        DEFB 06H,03H+80H  ;Kursor an Zeilenanfang + Zeile ab Kursor loeschen
        LD   B,0CH 
L044E:  RST  18H 
        DEFB 09H+80H 
        DJNZ L044E 
        CALL L07F1 
        LD   DE,(KURPOS) 
        RST  18H 
        DEFM ">" 
        DEFB 20H+80H 
        LD   B,08H 
L045E:  LD   A,(HL) 
        INC  HL 
        RST  18H 
        DEFB 20H+80H 
        CALL L07EE 
        DJNZ L045E 
        RST  18H 
        DEFM "  *" 
        DEFB 20H+80H 
        LD   HL,(ARG2) 
        LD   B,08H 
L0471:  LD   A,(HL) 
        INC  HL 
        CALL L0487 
        JR   NC,L047A 
        LD   A,2EH 
L047A:  RST  10H 
        DJNZ L0471 
        LD   (ARG1),HL 
        RST  18H 
        DEFM " *" 
        DEFB 06H 
        DEFM " " 
        DEFB 06H+80H 
        RET 
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
L0487:  CP   5FH          ;Backspace/DEL mit 5FH
        ELSE              ;alle anderen Monitor-Versionen
L0487:  CP   7FH          ;Backspace/DEL mit 7FH
        ENDIF
        JR   Z,L0494 
        CP   20H 
        RET  C 
        CP   7FH 
        JR   NC,L0494 
L0492:  AND  A 
        RET 
;
L0494:  SCF 
        RET 
;
L0496:  CP   47H 
        JR   NC,L0494 
        CP   30H 
        RET  C 
        CP   3AH 
        JR   C,L0492 
        CP   41H 
        RET 
;
L04A4:  RST  18H 
        DEFB 06H+80H 
        LD   B,0CH 
L04A8:  RST  18H 
        DEFB 20H+80H 
        DJNZ L04A8 
        RET 
;
L04AD:  EXX 
        LD   A,(DE) 
        AND  A 
        JR   Z,L04B5 
        CP   02H 
        LD   A,(HL) 
L04B5:  INC  DE 
        INC  HL 
        EXX 
        RET 
;
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        DEFS 17,0FFH      ;Platzhalter 17 Byte
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
        DEFS 17,0FFH      ;Platzhalter 17 Byte
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
;
; Funktion "Y" --> alternativer Monitor im RAM
;
        DEFB 00H,09H,59H,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC
        LD   B,10H        ;BC = 1000H, DE = 0000H
        LDIR 
        LD   HL,16D3H     ;OUT (P16),A --> RAM einblenden
        JP   ALTMO 
;
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
; Funktion "Y" --> alternativer Monitor im RAM
;
        DEFB 00H,09H,59H,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC
        LD   B,10H        ;BC = 1000H, DE = 0000H 
        LDIR 
        LD   HL,16D3H     ;OUT (P16),A --> RAM einblenden
        JP   ALTMO 
;
        ENDIF
;
;
; Register mit Argumenten laden
; -----------------------------
;
ARGLD:  LD   HL,(ARG1) 
        LD   DE,(ARG2) 
        LD   BC,(ARG3) 
        RET 
;
        DEFS 2,0FFH       ;Platzhalter 2 Byte
;
; Funktion "P" --> Pattern
;
        DEFB 00H,09H,50H,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC 
        LD   (HL),C       ;Pattern --> (HL)
        PUSH HL 
        XOR  A            ;wegen SBC-Befehl
        EX   DE,HL 
        SBC  HL,DE        ;Wert fuer BC berechnen
        LD   B,H 
        LD   C,L 
        POP  HL           ;HL zurueck
        INC  DE           ;DE = HL + 1
JPLDIR: LDIR 
        RET 
;
;
; Funktion "T" --> Transfer
;
        DEFB 00H,09H,54H,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC 
        XOR  A            ;wegen SBC-Befehl
        PUSH HL 
        SBC  HL,DE        ;Carry-Flag wird bei HL < DE gesetzt
        POP  HL 
        JR   NC,JPLDIR    ;Sprung zum LDIR-Befehl (HL >= DE)
        ADD  HL,BC
        EX   DE,HL 
        ADD  HL,BC 
        EX   DE,HL 
        DEC  HL           ;HL = HL + BC - 1
        DEC  DE           ;DE = DE + BC - 1
        LDDR              ;HL und DE zaehlen nach unten
        RET 
;
;
; Funktion "A" --> Arithmetik (TODO)
;
        DEFB 00H,09H,41H,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC 
        PUSH HL 
        PUSH DE 
        INC  HL 
        INC  HL 
        EX   DE,HL 
        XOR  A 
        SBC  HL,DE 
        JR   C,L051D 
        CP   H 
        JR   NZ,L052F 
        BIT  7,L 
        JR   NZ,L052F 
        JR   L0525 
;
L051D:  CPL 
        CP   H 
        JR   NZ,L052F 
        BIT  7,L 
        JR   Z,L052F 
L0525:  RST  18H 
        DEFM "DSPL" 
        DEFB 3AH+80H      ;":+80H 
        LD   A,L 
        CALL OUTHEX 
L052F:  POP  DE 
        POP  HL 
        PUSH HL 
        PUSH HL 
        ADD  HL,DE 
        RST  18H 
        DEFM "   SUM" 
        DEFB 3AH+80H      ;":+80H 
        CALL OUTHL 
        POP  HL 
        SBC  HL,DE 
        RST  18H 
        DEFM "   DIF" 
        DEFB 3AH+80H      ;":+80H 
        CALL OUTHL 
        POP  BC 
        LD   HL,0000H 
        LD   E,L 
L0552:  LD   A,B 
        OR   C 
        JR   Z,L0565 
        DEC  BC 
        LD   A,L 
        INC  A 
        DAA 
        LD   L,A 
        LD   A,H 
        ADC  A,00H 
        DAA 
        LD   H,A 
        JR   NC,L0552 
        INC  E 
        JR   L0552 
;
L0565:  RST  18H 
        DEFM "   DEC" 
        DEFB 3AH+80H      ;":+80H 
        LD   A,E 
        AND  0FH 
        OR   30H 
        RST  10H 
        CALL OUTHL 
        RST  18H 
        DEFB 0DH+80H 
        RET 
;
;
; ??? (TODO)
;
L0579:  LD   C,A 
        LD   HL,05BEH 
        LD   B,08H 
L057F:  RLC  C 
        JR   NC,L0585 
        LD   A,(HL) 
        RST  10H 
L0585:  INC  HL 
        DJNZ L057F 
        RET 
;
;
; UP OUTHL1
; gibt das HL-Register als vierstellige Hexzahl auf dem Schirm aus
;

OUTHL1: CALL OUTHL        ;HL auf dem Bildschirm ausgeben
OUTHL2: RST  18H          ;4 Leerzeichen auf dem BS ausgeben
        DEFM "   " 
        DEFB 20H+80H 
        RET 
;
;
; Texte (TODO)
;
        DEFB 41H,42H,44H,48H ;ABDH
        DEFB 58H,59H,43H,50H ;XYCP
        DEFB 20H,20H,20H,20H
        DEFB 53H,50H,50H,43H ;SPPC
        DEFB 49H,59H,49H,58H ;IYIX
        DEFB 4DH,41H,49H,4EH ;MAIN
        DEFB 48H,4CH,44H,45H ;HLDE
        DEFB 42H,43H,41H,46H ;BCAF
        DEFB 45H,58H,58H,52H ;EXXR
        DEFB 48H,4CH,44H,45H ;HLDE
        DEFB 42H,43H,41H,46H ;BCAF
        DEFB 53H,5AH,00H,48H ;SZ.H
        DEFB 00H,50H,4EH,43H ;.PNC
;
;
; Funktion "R" --> Register (TODO)
;
        DEFB 00H,09H,52H,0DH 
        CP   3AH 
        JR   NZ,L063D 
L05CE:  RST  18H 
        DEFM "  BP" 
        DEFB 3AH+80H      ;":+80H 
        LD   HL,(1879H) 
        CALL OUTHL1       ;HL auf dem Bildschirm ausgeben
        RST  18H 
        DEFM "BS" 
        DEFB 3AH+80H      ;":+80H 
        LD   HL,187BH 
        LD   B,03H 
L05E3:  LD   A,(HL) 
        CALL OUTHEX 
        INC  HL 
        DJNZ L05E3 
        LD   C,03H 
        LD   DE,059AH 
        LD   IX,1878H 
L05F3:  RST  18H 
        DEFB 0DH 
        DEFM " " 
        DEFB 20H+80H 
        LD   B,04H 
L05F9:  LD   A,(DE) 
        RST  10H 
        INC  DE 
        DJNZ L05F9 
        RST  18H 
        DEFM " " 
        DEFB 20H+80H 
        LD   B,04H 
L0603:  LD   A,(DE) 
        RST  10H 
        INC  DE 
        LD   A,(DE) 
        RST  10H 
        INC  DE 
        RST  18H 
        DEFB 3AH+80H      ;":+80H 
        DEFB 0DDH,66H,00H ;Befehl = LD H,(IX+d)
        DEC  IX 
        DEFB 0DDH,6EH,00H ;Befehl = LD L,(IX+d)
        DEC  IX 
        CALL OUTHL1       ;HL auf dem Bildschirm ausgeben
        DJNZ L0603 
        DEC  C 
        JR   NZ,L05F3 
        RST  18H 
        DEFB 0DH 
        DEFM "  FLAGS: " 
        DEFB 20H+80H 
        LD   A,(1869H) 
        CALL L0579 
        RST  18H 
        DEFM "  " 
        DEFB 28H+80H      ;"(+80H 
        LD   A,(1861H) 
        CALL L0579 
        RST  18H 
        DEFM ")" 
        DEFB 0DH+80H 
        RET 
;
;
; ??? (TODO)
;
L063D:  LD   HL,(ZWINL) 
        DEC  HL 
        DEC  HL 
        LD   A,(HL) 
        PUSH HL 
        LD   HL,0592H 
        LD   B,04H 
        LD   DE,1869H 
L064C:  CP   (HL) 
        JR   Z,L0665 
        INC  DE 
        INC  DE 
        INC  HL 
        DJNZ L064C 
        POP  BC 
        DEC  BC 
        LD   A,(BC) 
        LD   B,04H 
L0659:  CP   (HL) 
        JR   Z,L0673 
        INC  DE 
        INC  DE 
        INC  HL 
        DJNZ L0659 
        JP   L05CE 
;
        DEFB 0FFH         ;Platzhalter 1 Byte
;
;
; ??? (TODO)
;
L0665:  POP  HL 
        DEC  HL 
        DEC  HL 
        LD   A,(HL) 
        EX   DE,HL 
        CP   27H 
        JR   NZ,L0672 
        LD   DE,0FFF8H
        ADD  HL,DE 
L0672:  EX   DE,HL 
L0673:  LD   A,(DE) 
        LD   L,A 
        INC  DE 
        LD   A,(DE) 
        LD   H,A 
        RST  18H 
        DEFM " " 
        DEFB 20H+80H 
        CALL OUTHL 
; dieses UP liest die Eingabezeile auf dem Monitor,
; wandelt den eingegebenen ASCII-Wert (4 Stellen) in 
; eine 16-bit HEX-Zahl und speichert die Eingabe
; (wenn etwas eingegeben wurde) in (DE-1) und (DE)
INHEX4: CALL INLINE 
        LD   HL,(ZWINL)   ;Adresse Zeilenanfang in HL
        EX   DE,HL
        PUSH HL           ;DE = Speicheradresse + 1
        PUSH DE           ;Adresse BS-Zeilenanfang
        CALL INHEX        ;Umwandlung ASCII --> HEX
        EX   DE,HL        ;HEX-Ergebnis jetzt in DE
        POP  HL           ;Adresse BS-Zeilenanfang
        LD   A,(HL)       ;1. Zeichen ASCII-Eingabe vom BS lesen
        POP  HL           ;HL = Speicheradresse + 1
        CP   20H          ;Leerzeichen?
        RET  Z            ;es wurde kein Wert eingegeben
        LD   (HL),D       ;eingegebenen Wert speichern
        DEC  HL 
        LD   (HL),E 
        RET 
;
;
; Funktion "C" --> Compare
;
        DEFB 00H,09H,43H,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC 
COMP1:  LD   A,(DE) 
        CP   (HL) 
        JR   Z,COMP2      ;(HL) und (DE) sind identisch
        RST  18H          ;Ausgabe der Differenz:
        DEFM " " 
        DEFB 20H+80H 
        CALL OUTHL1       ;HL auf dem Bildschirm ausgeben
        LD   A,(HL) 
        CALL OUTHEX       ;(HL) auf dem Bildschirm ausgeben
        CALL OUTHL2       ;4 Leerzeichen auf dem BS ausgeben
        CALL OUTHL2       ;dto.
        EX   DE,HL 
        CALL OUTHL1       ;DE auf dem Bildschirm ausgeben
        EX   DE,HL 
        LD   A,(DE) 
        CALL OUTHEX       ;(DE) auf dem Bildschirm ausgeben
        RST  18H 
        DEFB 0DH+80H      ;neue Zeile und Tastaturabfrage:
        RST  08H
        CP   0DH          ;<ENTER> gedrueckt?
        RET  NZ           ;nein --> die Routine beenden
COMP2:  DEC  BC           ;ja --> weiter vergleichen
        INC  HL 
        INC  DE           ;HL++; DE++; BC--;
        LD   A,B 
        OR   C 
        RET  Z            ;Ende bei BC = 0
        JR   COMP1        ;naechster Vergleich
;
;
; Funktion "B" --> Breakpoint
;
        DEFB 00H,09H,42H,0DH 
        LD   HL,(ARG1) 
L06CE:  LD   (1879H),HL   ;BP-Adresse merken
        LD   DE,187BH     ;Merkzellen fuer urspruenglichen Code
        LD   BC,0003H     ;3 Bytes ab BP sichern
        LDIR 
        LD   HL,RUNBP     ;Routine fuer Breakpoint-Bearbeitung
        LD   (1815H),HL   ;Sprungtabelle fuer RST38 patchen
        RET 
;
;
; Funktion "G" --> Go
;
        DEFB 00H,09H,47H,0DH
        LD   HL,(1875H)   ;PC-Register --> HL
        LD   (ARG1),HL    ;als ARG1 speichern und weiter mit "J"
;
;
; Funktion "J" --> Jump
;
        DEFB 00H,09H,4AH,0DH
        LD   A,(1815H)    ;nachschauen, wie die Sprungtab. f. RST38 aussieht
        CP   40H          ;Test, ob "BREAK" (0240H) eingetragen ist
        JR   Z,JUMP1      ;ja, es wurde also kein Breakpoint gesetzt
        LD   HL,(1879H)   ;nein, lade Breakpoint-Adresse
        LD   (HL),0FFH    ;setze "RST38" auf Breakpoint-Adresse
JUMP1:  LD   HL,(1877H)   ;lade SP-Adresse
        LD   B,10         ;Teste fuer den Stackpointer 10 Bytes abwaerts, 
        DEC  HL           ;ob er im RAM oder auf dem EPROM unterwegs ist...
JUMP2:  LD   A,(HL) 
        CPL               ;Bitweises Negieren von A
        LD   (HL),A 
        CP   (HL) 
        JR   NZ,JUMP3     ;das ist 'ne EPROM-Zelle --> SP = 2000H setzen!
        CPL               ;alles wieder zurueckdrehen
        LD   (HL),A 
        DJNZ JUMP2 
        LD   HL,(1877H)   ;alles RAM-Speicher --> SP = (1877H)
        DEFB 0DDH         ;LD IX,2000H (+ nxt. Befehl ueberspringen, genial!)
JUMP3:  LD   HL,2000H
        LD   SP,HL        ;SP setzen
        LD   HL,(ARG1) 
        LD   (1875H),HL   ;HL-->PC
        PUSH HL           ;Startadresse in den Stack
        JP   RSACPU       ;UP, CPU-Register einraeumen von RSA
;
;
; Funktion "E" --> Einzelschritt
;
        DEFB 00H,09H,45H,0DH 
        LD   A,(DE)       ;DE = Befehszeile
        CP   3AH          ;Eingabe ":"?
        JR   Z,EINZ2      ;ja, Befehlsabarbeitung ab PC
        LD   HL,(ARG1)    ;nein, Befehlsabarbeitung ab ARG1
EINZ1:  LD   (1875H),HL 
EINZ2:  LD   SP,(1877H)   ;RSA: SP
        LD   HL,(1875H)   ;RSA: PC
        PUSH HL 
        CALL RSACPU       ;UP, CPU-Register einraeumen von RSA 
        PUSH AF 
        LD   A,87H        ;CTC Kanal 0: Interrupt ein, Zeitgeber, VT=16
        OUT  (P00),A 
        LD   A,02H        ;CTC Kanal 0: Teiler=2
        OUT  (P00),A 
        EI 
        LD   A,(BC)       ;Zeit verbraten bis zum Interrupt?
        POP  AF 
        RET 
;
;
; Routine fuer Breakpoint
;
RUNBP:  CALL CPURSA       ;UP, CPU-Register ausraeumen nach RSA 
        LD   HL,(0233H)   ;orig. Einsprungpunkt fuer RST38
        LD   (1815H),HL   ;in den Sprungverteiler zurueckschreiben
        POP  HL           ;PC nach RST38 aus dem Stack holen
        DEC  HL 
        LD   A,(187BH)    ;1. Byte der BP-Sequenz 
        LD   (HL),A       ;urspr. 1. Byte zurueckschreiben 
        LD   (1877H),SP   ;SP sichern
        JR   EINZ1 
;
        DEFS 3,0FFH       ;Platzhalter 3 Byte
;
;
; ??? (TODO)
;
        CALL CPURSA       ;UP, CPU-Register ausraeumen nach RSA 
        POP  DE 
        LD   (1877H),SP 
        LD   HL,(1875H) 
        LD   (1875H),DE 
        LD   SP,1856H     ;Stack 
        RST  18H 
        DEFB 06H,20H+80H  ;Kursor an Zeilenanfang + 1 Leerzeichen
        CALL OUTHL 
        LD   B,05H 
L0775:  RST  18H 
        DEFB 20H+80H 
        PUSH HL 
        XOR  A 
        SBC  HL,DE 
        POP  HL 
        JR   Z,L0785 
L077E:  LD   A,(HL) 
        INC  HL 
        CALL OUTHEX 
        JR   L078D 
;
L0785:  LD   A,05H 
        CP   B 
        JR   Z,L077E 
        RST  18H 
        DEFM " " 
        DEFB 20H+80H 
L078D:  DJNZ L0775 
        LD   B,04H 
        LD   DE,05AAH 
        LD   HL,1870H 
L0797:  RST  18H 
        DEFB 20H+80H 
        LD   A,(DE) 
        RST  10H 
        INC  DE 
        LD   A,(DE) 
        RST  10H 
        INC  DE 
        RST  18H 
        DEFB 3AH+80H      ;":+80H 
        LD   A,(HL) 
        DEC  HL 
        PUSH HL 
        LD   L,(HL) 
        LD   H,A 
        CALL OUTHL 
        POP  HL 
        DEC  HL 
        DJNZ L0797 
        RST  18H 
        DEFM " F" 
        DEFB 3AH+80H      ;":+80H 
        LD   A,(1869H) 
        CALL L0579 
        RST  18H 
        DEFB 0DH,0A0H 
        LD   HL,(1875H) 
        CALL OUTHL 
        LD   B,05H 
L07C2:  RST  18H 
        DEFB 20H+80H 
        LD   A,(HL) 
        INC  HL 
        CALL OUTHEX 
        DJNZ L07C2 
        LD   A,03H 
        OUT  (P00),A 
L07CF:  RST  10H 
L07D0:  RST  08H 
        LD   HL,06E4H 
        CP   47H 
        JR   Z,L0804 
        LD   HL,072BH 
        CP   0DH 
        JR   Z,L0804 
        CP   20H 
        JR   C,L07CF 
        LD   HL,01DEH     ;Adresse Kommandozeicheneingabe
        JR   L0800 
;
;
; Sprungverteiler
;
L07E8:  JP   V24OUT       ;V.24 Ausgabe
L07EB:  JP   MS30         ;MS 30
L07EE:  JP   OUTHEX       ;OUTHEX
L07F1:  JP   OUTHL        ;OUT HL
L07F4:  JP   INLINE       ;INLINE
L07F7:  JP   INHEX        ;INHEX
L07FA:  JP   TASTE        ;TASTE
L07FD:  JP   GETCO        ;GETCO
;
;
; Fortsetzung von oben
;
L0800:  CP   51H 
        JR   NZ,L0807 
L0804:  PUSH HL 
        RETI 
;
L0807:  CP   42H 
        JR   NZ,L0823 
        RST  18H 
        DEFM " B" 
        DEFB 50H+80H      ;"P+80H 
        CALL INLINE 
        LD   HL,(ZWINL) 
        LD   A,(HL) 
        CP   20H 
        JR   Z,L07D0 
        EX   DE,HL 
        CALL INHEX 
        CALL L06CE 
        JR   L07D0 
;
L0823:  CP   52H 
        CALL Z,INLINE 
        CALL Z,L063D 
        JR   L07D0 
;
;
; Funktion "D" --> Dump (TODO)
;
        DEFB 00H,09H,44H,0DH 
        LD   IX,(ARG2) 
        XOR  A 
        LD   (ARG3),A 
        LD   HL,ARG2 
        LD   A,(185EH) 
        OR   (HL) 
        LD   (1860H),A 
        RST  18H 
        DEFM "*" 
        DEFB 0DH+80H 
L0846:  LD   C,01H 
        LD   HL,(ARG1) 
        LD   A,L 
        AND  0F8H
        LD   L,A 
        CALL L0441 
        LD   A,(1860H) 
        AND  A 
        JR   Z,L0867 
        RST  18H 
        DEFB 0DH+80H 
        LD   DE,(ARG1) 
        PUSH IX 
        POP  HL 
        AND  A 
        SBC  HL,DE 
        RET  C 
        JR   L0846 
;
L0867:  CALL L07FA 
        JR   Z,L0873 
        RST  18H 
        DEFB 0DH+80H 
        JR   L0846 
;
L0870:  CALL L0441 
L0873:  LD   HL,(ARG2) 
        ADD  HL,BC 
        DEC  HL 
        RST  18H 
        DEFM " " 
        DEFB 3CH+80H      ;"<+80H 
        CALL L07F1 
        RST  18H 
        DEFB 3EH+80H      ;">+80H 
        LD   A,(ARG3) 
        AND  A 
        JR   NZ,L088E 
        RST  18H 
        DEFM " (H)" 
        DEFB 20H+80H 
        JR   L0894 
;
L088E:  RST  18H 
        DEFM " (A)" 
        DEFB 20H+80H 
L0894:  LD   (KURPOS),DE 
        LD   B,C 
L0899:  RST  18H 
        DEFB 09H,09H,09H+80H 
        DJNZ L0899 
        CALL L07FA 
        CP   8BH 
        JR   Z,L0903 
L08A6:  RST  08H 
        CP   08H 
        JP   Z,L0932 
        CP   03H 
        JR   Z,L08F3 
        CP   0DH 
        JR   Z,L08EB 
        CP   01H 
        JR   Z,L08F9 
        CP   09H 
        JR   Z,L091B 
        CP   0AH 
        JR   Z,L092C 
        CP   0BH 
        JR   Z,L0903 
        CP   20H 
        JR   C,L08A6 
        EX   AF,AF' 
        LD   A,(ARG3) 
        AND  A 
        JR   NZ,L090B 
        EX   AF,AF' 
        CALL L0496 
        JR   C,L08A6 
        LD   B,02H 
L08D7:  RST  10H 
        CP   3AH 
        JR   C,L08DE 
        SUB  37H 
L08DE:  RLD 
        DEC  B 
        JR   Z,L0912 
L08E3:  RST  08H 
        CALL L0496 
        JR   C,L08E3 
        JR   L08D7 
;
L08EB:  CALL L04A4 
        RST  18H 
        DEFB 0DH+80H 
        JP   L0846 
;
L08F3:  RST  18H 
        DEFB 0DH+80H 
        LD   (ARG1),HL 
        RET 
;
L08F9:  LD   A,(ARG3) 
        CPL 
        LD   (ARG3),A 
        JP   L0870 
;
L0903:  LD   DE,0008H 
        AND  A 
        SBC  HL,DE 
        JR   L0938 
;
L090B:  EX   AF,AF' 
        CALL L0487 
        JR   C,L08A6 
        LD   (HL),A 
L0912:  LD   A,08H 
        CP   C 
        JR   NZ,L091B 
        CALL L0441 
        DEC  HL 
L091B:  INC  C 
        INC  HL 
        LD   A,09H 
        CP   C 
        JR   NZ,L0929 
        LD   C,01H 
L0924:  CALL L04A4 
        RST  18H 
        DEFB 0AH+80H 
L0929:  JP   L0870 
;
L092C:  LD   DE,0008H 
        ADD  HL,DE 
        JR   L0924 
;
L0932:  DEC  HL 
        DEC  C 
        JR   NZ,L0929 
        LD   C,08H 
L0938:  CALL L04A4 
        LD   DE,(KURPOS) 
        PUSH HL 
        LD   HL,17BFH 
        SBC  HL,DE 
        POP  HL 
        JR   NC,L0957 
        EXX 
        LD   HL,1040H 
        LD   DE,1000H 
        LD   BC,07C0H 
        LDIR 
        EXX 
        JR   L0929 
;
L0957:  RST  18H 
        DEFB 0BH+80H 
        JR   L0929 
;
;
; Funktion "F" --> Find (TODO)
;
        DEFB 00H,09H,46H,0DH 
        CALL L0A61 
        LD   A,(DE) 
        CP   4AH 
        JR   Z,L0999 
        CP   27H 
        JR   Z,L096D 
        JR   L098E 
;
L096D:  DEC  DE 
        LD   A,(DE) 
        CP   20H 
        JR   Z,L097D 
        EXX 
        LD   (HL),A 
        INC  HL 
        LD   A,01H 
        LD   (DE),A 
        INC  DE 
        EXX 
        JR   L096D 
;
L097D:  DEC  DE 
        LD   A,(DE) 
        CP   4AH 
        JR   Z,L0999 
        CP   27H 
        JR   Z,L096D 
        CP   20H 
        JR   Z,L09A2 
        CALL L07F7 
L098E:  LD   A,L 
        EXX 
        LD   (HL),A 
        INC  HL 
        LD   A,01H 
        LD   (DE),A 
        INC  DE 
        EXX 
        JR   L097D 
;
L0999:  EXX 
        INC  HL 
        XOR  A 
        LD   (DE),A 
        INC  DE 
        EXX 
        DEC  DE 
        JR   L097D 
;
L09A2:  EXX 
        LD   A,0FFH
        LD   (DE),A 
        EXX 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC 
        LD   BC,0164H 
L09AD:  CALL L0A61 
        CALL L04AD 
L09B3:  PUSH AF 
        AND  A 
        PUSH HL 
        SBC  HL,DE 
        POP  HL 
        JR   NC,L09C4 
        POP  AF 
        JR   Z,L09DE 
        CP   (HL) 
        JR   Z,L09DE 
        INC  HL 
        JR   L09B3 
;
L09C4:  POP  AF 
L09C5:  CALL L0A61 
        EXX 
        CALL L0A6A 
        DEC  B 
        JP   NZ,L09DB 
        RST  18H 
        DEFM " Not foun" 
        DEFB 64H+80H      ;"d+80H 
L09DB:  RST  18H 
        DEFB 0DH+80H 
        RET 
;
L09DE:  PUSH HL 
        POP  IX 
        LD   A,18H 
        CP   H 
        JR   NZ,L09EB 
        LD   A,0D0H
        CP   L 
        JR   Z,L09F6 
L09EB:  INC  HL 
        CALL L04AD 
        JR   Z,L09EB 
        JR   NC,L09FC 
        CP   (HL) 
        JR   Z,L09EB 
L09F6:  PUSH IX 
        POP  HL 
        INC  HL 
        JR   L09AD 
;
L09FC:  PUSH IX 
        POP  HL 
        XOR  A 
        ADD  A,B 
        JR   Z,L0A06 
        LD   (ARG1),HL 
L0A06:  RST  18H 
        DEFB 20H+80H 
        CALL OUTHL 
        RST  18H 
        DEFB 3AH+80H      ;":+80H 
        LD   B,04H 
L0A0F:  LD   A,(HL) 
        CALL OUTHEX 
        INC  HL 
        DJNZ L0A0F 
        RST  18H 
        DEFM " " 
        DEFB 20H+80H 
        PUSH IX 
        POP  HL 
        INC  HL 
        DEC  C 
        JR   NZ,L09AD 
        RST  08H 
        CP   51H 
        JR   Z,L09C5 
        LD   C,64H 
        JR   L09AD 
;
        DEFS 2,0FFH       ;Platzhalter 2 Byte
;
L0A2B:  LD   HL,(ARG1) 
        JP   Z,L0DF0 
L0A31:  IN   A,(P05) 
        RES  5,A 
        JR   L0A3C 
;
L0A37:  IN   A,(P05) 
        SET  5,A 
        DI 
L0A3C:  RST  18H 
        DEFB 07H+80H 
        OUT  (P05),A 
        RET 
;
;
; Funktion "H" --> I/O-Byte anzeigen / setzen
;
        DEFB 00H,09H,48H,0DH 
        RST  18H 
        DEFM " I/O Byte" 
        DEFB 20H+80H 
        LD   DE,IOBYTE    ;Speicherplatz vom I/O-Byte 
        LD   A,(DE)
        CALL OUTHEX       ;Bildschirmausgabe I/O-Byte
        INC  DE 
        JP   INHEX4       ;Eingabe pruefen und I/O-Byte setzen
;
L0A5B:  CALL L0404 
        JP   L0A31 
;
L0A61:  EXX 
        LD   HL,18D0H 
        LD   DE,18E8H 
        EXX 
        RET 
;
L0A6A:  LD   (HL),C 
        PUSH HL 
        XOR  A 
        EX   DE,HL 
        SBC  HL,DE 
        LD   B,H 
        LD   C,L 
        POP  HL 
        DEC  DE 
        LDIR 
        EXX 
        RET 
;
        DEFS 3,0FFH       ;Platzhalter 3 Byte
;
L0A7B:  LD   HL,023AH 
        LD   B,03H 
L0A80:  LD   A,(DE) 
        CP   (HL) 
        RET  NZ 
        INC  HL 
        INC  DE 
        DJNZ L0A80 
        LD   A,(DE) 
        LD   L,A 
        INC  DE 
        LD   A,(DE) 
        LD   H,A 
        RET 
;
;
; Einsprungpunkt fuer RST 18H   (Zeichenkettenausgabe)
; ---------------------------
;
RST18:  EX   (SP),HL 
        PUSH AF 
RST18A: LD   A,(HL) 
        INC  HL 
        RST  10H 
        RLCA 
        JR   NC,RST18A 
        POP  AF 
        EX   (SP),HL 
        RET 
;
;
; ??? (TODO)
;
        OUT  (P05),A 
        RET 
;
        DEFS 4,0FFH       ;Platzhalter 4 Byte
;
; Funktion "I" --> Initialize
;
        DEFB 00H,09H,49H,0DH 
        LD   HL,1858H 
        LD   B,26H 
INIRSA: LD   (HL),00H     ;RAM-Zellen von 1858H bis 187DH
        INC  HL           ;mit 00H beschreiben
        DJNZ INIRSA 
        RST  18H 
        DEFM " SP"        ;= Stack Pointer
        DEFB 20H+80H 
        CALL UPRAMT       ;HL = letzte RAM-Zelle + 1
        LD   (1877H),HL   ;Anwenderstack festlegen
        JR   OUTHL3       ;HEX-Adresse auf BS ausgeben
;
;
; Funktion "O" --> RAM-Test
;
        DEFB 00H,09H,4FH,0DH 
        LD   HL,(ARG1)    ;ARG1 laden
        RST  18H 
        DEFM " ME"        ;= Memory End
        DEFB 20H+80H 
        CALL UPRAMT       ;UP RAM-Test
        DEC  HL           ;Adresse der letzten RAM-Zelle
OUTHL3: JP   OUTHL        ;auf BS ausgeben
;
        DEFB 0FFH         ;Platzhalter 1 Byte
;
; UP RAM-Test
;
UPRAMT: LD   A,(HL) 
        CPL               ;Akku bitweise negieren
        LD   (HL),A       ;Komplement zurueckschreiben
        CP   (HL) 
        RET  NZ           ;Abbruch bei Fehler
        CPL               ;Akku bitweise negieren
        LD   (HL),A       ;Originalwert zurueckschreiben
        INC  HL           ;naechste Speicheradresse
        JR   UPRAMT       ;LOOP
;
;
; Tabelle fuer IO-Befehle (17x Port, Befehl, Port, Befehl, ...)
;
TABIO:  DEFB P00,0F8H     ;CTC Interruptvektor NWT = 0F8H
        DEFB P01,37H      ;CTC Kanal 1: kein Interrupt, Zeitgeber, VT=256
        DEFB P01,4EH      ;CTC Kanal 1: Teiler=78 --> 100 Hz (bei 2 MHz CPU)
        DEFB P02,47H      ;CTC Kanal 2: kein Interrupt, Zaehlermode
        DEFB P02,32H      ;CTC Kanal 2: Teiler=50 --> 2 Hz (bei 2 MHz CPU)
        DEFB P0B,0CFH     ;PIO2 Port B Ctrl: Mode 3
        DEFB P0B,00H      ;PIO2 Port B Ctrl: alles Ausgaenge
        DEFB P0A,0CFH     ;PIO2 Port A Ctrl: Mode 3
        DEFB P0A,0C5H     ;PIO2 Port A Ctrl: A0, A2, A6, A7 sind Eingaenge
        DEFB P08,0AH      ;PIO2 Port A Data: A1 und A3 auf High setzen
        DEFB P06,0CFH     ;PIO1 Port A Ctrl: Mode 3
        DEFB P06,0FFH     ;PIO1 Port A Ctrl: alles Eingaenge (von Tastatur)
        DEFB P06,07H      ;PIO1 Port A Ctrl: kein Interrupt
        DEFB P07,0CFH     ;PIO1 Port B Ctrl: Mode 3
        DEFB P07,84H      ;PIO1 Port B Ctrl: B7 (TB in) und B2 (Grafikt.) sind Eingaenge
        DEFB P07,07H      ;PIO1 Port B Ctrl: kein Interrupt
        DEFB P14,00H      ;Modul 1: Konfigurationsregister ruecksetzen
        DEFB 0FFH
;
;
; Funktion "K" --> Kassette save (TODO)
;
        DEFB 00H,09H,4BH,0DH 
        CALL L0B97 
        CALL L0A37 
        DEC  HL 
        LD   (187EH),HL 
        LD   HL,1896H 
        LD   (1880H),HL 
        CALL L0BE1 
        CALL L0BF7 
        LD   DE,187EH 
        LD   HL,18B1H 
        LD   BC,0004H 
        LDIR 
        CALL L0BF7 
        JP   L0A5B 
;
;
; Funktion "Z" --> Kassette verify (TODO)
;
        DEFB 00H,09H,5AH,0DH 
        XOR  A 
        DEC  A 
        JR   L0B33 
;
;
; Funktion "L" --> Kassette load (TODO)
;
        DEFB 00H,09H,4CH,0DH 
        ADD  A,D 
L0B33:  EX   AF,AF' 
        CALL L0B97 
        CALL L0A37 
        LD   (1880H),HL 
        LD   HL,18D5H 
        LD   (187EH),HL 
L0B43:  CALL L0BE1 
L0B46:  CALL L0C81 
        LD   HL,18B6H 
        LD   DE,1896H 
        LD   B,08H 
L0B51:  LD   A,(DE) 
        CP   (HL) 
        JR   NZ,L0B46 
        INC  HL 
        INC  DE 
        DJNZ L0B51 
        LD   B,13H 
L0B5B:  LD   A,(DE) 
        CP   (HL) 
        JR   Z,L0B63 
        CP   20H 
        JR   NZ,L0B85 
L0B63:  INC  HL 
        INC  DE 
        DJNZ L0B5B 
        LD   E,7EH 
        LD   C,04H 
        LDIR 
        LD   HL,0DB7H 
        EX   AF,AF' 
        INC  A 
        JR   Z,L0B76 
        LD   L,0BDH
L0B76:  EX   AF,AF' 
        CALL L0D86 
        CALL L0C81 
        CALL L0DC9 
        RST  18H 
        DEFB 0DH+80H 
        JP   L0A2B 
;
L0B85:  CALL L0DC9 
        JR   NZ,L0B43 
        CALL L0D83 
        JR   L0B43 
;
        DEFB 0FFH         ;Platzhalter 1 Byte
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
;
; UP 'Zeitschleife 18 ms' zur Tastaturentprellung
; -----------------------------------------------
;
        PUSH BC
        LD   BC,0481H     ;Zeitkonstante fuer 18ms
        JP   MS30A
;
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
;
; UP 'Zeitschleife 18 ms' zur Tastaturentprellung
; -----------------------------------------------
;
        PUSH BC
        LD   BC,0481H     ;Zeitkonstante fuer 18ms
        JP   MS30A
;
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
;
; die Zeitschleife gibt es im Moni 2011 nicht mehr ...
;
        DEFB 0FFH
        DEFB 0FFH,0FFH,0FFH
        DEFB 0FFH,0FFH,0FFH
;
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
; die Zeitschleife gibt es im Moni 2021 nicht mehr ...
;
        DEFB 0FFH
        DEFB 0FFH,0FFH,0FFH
        DEFB 0FFH,0FFH,0FFH
;
        ENDIF
;
;
L0B97:  LD   HL,(ARG2) 
        LD   (18B1H),HL 
        LD   HL,(ARG1) 
        LD   (18B3H),HL 
        LD   C,50H 
L0BA5:  LD   A,(DE) 
        CP   3AH 
        DEC  DE 
        JR   Z,L0BA5 
        CP   2AH 
        JR   NZ,L0BB3 
        LD   A,(DE) 
        DEC  DE 
        LD   C,44H 
L0BB3:  CP   22H 
        LD   HL,1896H 
        LD   B,08H 
L0BBA:  LD   (HL),4EH 
        INC  HL 
        DJNZ L0BBA 
        LD   B,10H 
        JR   NZ,L0BCF 
L0BC3:  LD   A,(DE) 
        CP   22H 
        JR   Z,L0BCF 
        LD   (HL),A 
        DEC  DE 
        INC  HL 
        DJNZ L0BC3 
        JR   L0BD4 
;
L0BCF:  LD   (HL),20H 
        INC  HL 
        DJNZ L0BCF 
L0BD4:  LD   (HL),3AH 
        INC  HL 
        LD   (HL),20H 
        INC  HL 
        LD   (HL),C 
        LD   L,0B5H
        LD   (HL),4CH 
        INC  HL 
        RET 
;
L0BE1:  RST  18H 
        DEFB 0DH+80H 
        LD   HL,0DB1H 
L0BE6:  RST  18H 
        DEFB 0EH 
        DEFM "31" 
        DEFB 11H+80H 
        LD   B,06H 
L0BED:  LD   A,(HL) 
        RST  10H 
        INC  HL 
        DJNZ L0BED 
        RST  18H 
        DEFM " " 
        DEFB 10H,0A0H 
        RET 
;
L0BF7:  LD   HL,(1880H) 
        CALL L0C0B 
L0BFD:  EX   DE,HL 
        LD   HL,(187EH) 
        AND  A 
        SBC  HL,DE 
        EX   DE,HL 
        RET  C 
        CALL L0C10 
        JR   L0BFD 
;
L0C0B:  LD   DE,07D0H 
        JR   L0C13 
;
L0C10:  LD   DE,000EH 
L0C13:  LD   B,48H 
L0C15:  DJNZ L0C15 
        CALL L0C7A 
        DEC  DE 
        LD   A,E 
        OR   D 
        JR   NZ,L0C13 
        LD   C,02H 
L0C21:  LD   B,21H 
L0C23:  DJNZ L0C23 
        CALL L0C7A 
        DEC  C 
        LD   E,L 
        LD   D,H 
        JR   NZ,L0C21 
        NOP 
        PUSH DE 
        POP  IX 
        LD   B,08H 
L0C33:  DJNZ L0C33 
        CALL L0C59 
        LD   B,05H 
L0C3A:  DJNZ L0C3A 
        LD   C,10H 
L0C3E:  LD   E,(HL) 
        INC  HL 
        LD   D,(HL) 
        ADD IX,DE 
        INC  HL 
        PUSH BC 
        CALL L0C59 
        POP  BC 
        DEC  C 
        JR   Z,L0C52 
        LD   B,03H 
L0C4E:  DJNZ L0C4E 
        JR   L0C3E 
;
L0C52:  PUSH IX 
        POP  DE 
        LD   B,07H 
L0C57:  DJNZ L0C57 
L0C59:  LD   C,10H 
L0C5B:  SRL  D 
        RR   E 
        JR   NC,L0C68 
        LD   B,03H 
L0C63:  DJNZ L0C63 
        NOP 
        JR   L0C6B 
;
L0C68:  CALL L0C7A 
L0C6B:  LD   B,0FH 
L0C6D:  DJNZ L0C6D 
        CALL L0C7A 
        DEC  C 
        RET  Z 
        LD   B,0BH 
L0C76:  DJNZ L0C76 
        JR   L0C5B 
;
L0C7A:  IN   A,(P05) 
        XOR  40H 
        OUT  (P05),A 
        RET 
;
L0C81:  LD   IY,1882H 
        LD   HL,(1880H) 
        EXX 
        LD   HL,0100H 
        EXX 
L0C8D:  CALL L0CBD 
        EXX 
        JR   NZ,L0C99 
        DEC  H 
        LD   H,01H 
        JR   Z,L0CAC 
        INC  L 
L0C99:  LD   H,01H 
        EXX 
        LD   (IY+00H),L 
        LD   (IY+01H),H 
        INC  IY 
        INC  IY
        DEFB 0FDH,7DH     ;= undokumentierter Befehl LD A,IYL
        SUB  94H 
        RET  Z 
        EXX 
L0CAC:  EXX 
        EX   DE,HL 
        LD   HL,(187EH) 
        AND  A 
        SBC  HL,DE 
        EX   DE,HL 
        RET  C 
        JR   L0C8D 
;
        DEFS 5,0FFH       ;Platzhalter 5 Byte
;
L0CBD:  CALL L0D5A 
        CALL L0D52 
        LD   C,07H 
L0CC5:  LD   DE,060BH 
        LD   A,02H 
L0CCA:  DEC  A 
        JR   NZ,L0CCA 
        CALL L0D5A 
L0CD0:  CALL L0D5A 
        JR   NZ,L0CBD 
        DEC  D 
        JR   NZ,L0CD0 
        DEC  C 
        JR   Z,L0CE7 
L0CDB:  IN   A,(P05) 
        XOR  B 
        BIT  7,A 
        JR   NZ,L0CC5 
        DEC  E 
        JR   NZ,L0CDB 
        JR   L0CBD 
;
L0CE7:  CALL L0D52 
        LD   A,2CH 
L0CEC:  DEC  A 
        JR   NZ,L0CEC 
        CALL L0D5A 
        JR   NZ,L0CE7 
        CALL L0D52 
        LD   A,12H 
L0CF9:  DEC  A 
        JR   NZ,L0CF9 
        CALL L0D64 
        LD   C,10H 
        PUSH DE 
        POP  IX 
        LD   A,0DH 
L0D06:  DEC  A 
        JR   NZ,L0D06 
L0D09:  CALL L0D64 
        ADD IX,DE 
        PUSH BC 
        LD   C,L 
        LD   B,H 
        LD   HL,(187EH) 
        XOR  A 
        SBC  HL,BC 
        LD   L,C 
        LD   H,B 
        POP  BC 
        JR   C,L0D32 
        EX   AF,AF' 
        JR   Z,L0D23 
        LD   (HL),E 
        INC  HL 
        LD   (HL),D 
        DEC  HL 
L0D23:  EX   AF,AF' 
        LD   A,E 
        CP   (HL) 
        INC  HL 
        LD   A,D 
        JR   NZ,L0D2D 
        CP   (HL) 
        JR   Z,L0D38 
L0D2D:  EXX 
        INC  H 
        EXX 
        JR   L0D38 
;
L0D32:  LD   A,04H 
L0D34:  DEC  A 
        JR   NZ,L0D34 
        DEC  HL 
L0D38:  INC  HL 
        LD   A,02H 
L0D3B:  DEC  A 
        JR   NZ,L0D3B 
        DEC  C 
        JR   NZ,L0D09 
        CALL L0D64 
        EX   DE,HL 
        PUSH IX 
        POP  BC 
        XOR  A 
        SBC  HL,BC 
        EX   DE,HL 
        RET 
;
TASTE:  CALL UPTAST       ;Taste gedrueckt?
        JR   L0DAD 
;
L0D52:  IN   A,(P05) 
        XOR  B 
        BIT  7,A 
        JR   Z,L0D52 
        RET 
;
L0D5A:  IN   A,(P05) 
        XOR  B 
        BIT  7,A 
        PUSH AF 
        XOR  B 
        LD   B,A 
        POP  AF 
        RET 
;
L0D64:  PUSH HL 
        LD   L,10H 
L0D67:  CALL L0D5A 
        JR   NZ,L0D6F 
        XOR  A 
        JR   L0D70 
;
L0D6F:  SCF 
L0D70:  RR   D 
        RR   E 
        CALL L0D52 
        DEC  L 
        JR   Z,L0D81 
        LD   A,12H 
L0D7C:  DEC  A 
        JR   NZ,L0D7C 
        JR   L0D67 
;
L0D81:  POP  HL 
        RET 
;
L0D83:  LD   HL,0DC3H 
L0D86:  CALL L0BE6 
        LD   HL,18BEH 
        LD   B,13H 
L0D8E:  LD   A,(HL) 
        RST  10H 
        INC  HL 
        DJNZ L0D8E 
        RST  18H 
        DEFM "   " 
        DEFB 20H+80H 
        LD   HL,(18D3H) 
        LD   (ARG1),HL 
        CALL OUTHL 
        RST  18H 
        DEFM " " 
        DEFB 20H+80H 
        LD   HL,(18D1H) 
        LD   (ARG2),HL 
        JP   OUTHL 
;
L0DAD:  RET  Z 
        SET  7,A 
        RET 
;
;
; Texte fuer Kassettenarbeit
;
        DEFM "WARTEN"
        DEFM "VERIFY"
        DEFM "LADEN "
        DEFM "FOUND "
;
;
; ??? (TODO)
;
L0DC9:  LD   DE,1882H
        DEFB 0FDH,7DH     ;= undokumentierter Befehl LD A,IYL
        SUB  E 
        RET  Z 
        RST  18H 
        DEFB 0DH+80H 
        CALL ERROR 
        RRCA 
        LD   B,A 
        EXX 
        SUB  L 
        EXX 
        JR   NZ,L0DE1 
        RST  18H 
        DEFM "RAM" 
        DEFB 3AH+80H      ;":+80H 
L0DE1:  LD   A,(DE) 
        INC  DE 
        LD   L,A 
        LD   A,(DE) 
        INC  DE 
        LD   H,A 
        RST  18H 
        DEFB 20H+80H 
        CALL OUTHL 
        DJNZ L0DE1 
        DEC  B 
        RET 
;
L0DF0:  CALL L0A31 
        EX   DE,HL 
        CALL L0A7B 
        RET  NZ 
        JP   (HL) 
;
;
; V.24 - Ausgabe UP
; -----------------
;
V24OUT: PUSH HL 
        PUSH DE 
        PUSH BC 
        PUSH AF 
        LD   HL,V24REG    ;V.24-Kontrollregister
        LD   BC,7FFFH 
        BIT  4,(HL)
        JR   NZ,V24OU1    ;--> 7 Datenbit, ungerade Paritaet
        BIT  5,(HL) 
        JR   Z,V24OU2     ;--> 8 Datenbit, keine Paritaet
        LD   C,B          ;else --> 7 Datenbit, gerade Paritaet
V24OU1: AND  B 
        JP   PE,V24OU2    ;Paritaet ist korrekt
        SET  7,A          ;else --> Paritaetsflag setzen
V24OU2: XOR  C            ;Zeichen negieren ...
        LD   C,A          ;... und nach C kopieren
        LD   A,(HL) 
        AND  07H          ;Selektion der Baudrate
        LD   B,A          ;B = Baudrate (kodiert)
        LD   A,01H 
V24OU3: ADD  A,A 
        DJNZ V24OU3 
        LD   D,A          ;19200 Baud --> D = 2
        BIT  6,(HL) 
        JR   NZ,V24OU5    ;kein Handshake
V24OU4: IN   A,(P08)      ;else --> mit Handshake
        BIT  2,A 
        JR   NZ,V24OU4    ;warten auf fallende Flanke an A2
V24OU5: LD   B,10         ;10 Durchlaeufe Bit-Ausgabe
        BIT  3,(HL)       ;Anzahl der Stopbits?
        JR   Z,V24OU6     ;1 Stopbit
        INC  B            ;2 Stopbits --> 11 Durchlaeufe
V24OU6: SCF               ;Carry-Flag setzen (Startbit)
V24OU7: LD   E,D          ;19200 Baud --> E = 2
        SET  1,A          ;TxD = High
        JR   NC,V24OU8    ;Carry-Flag auswerten
        RES  1,A          ;TxD = Low
V24OU8: OUT  (P08),A      ;Ausgabe an A1 (TxD)
        LD   L,(HL)       ;Baudratenanpassung fuer 2 MHz
        INC  IX           ;Baudratenanpassung fuer 2 MHz
        DEC  E 
        JR   NZ,V24OU8 
        SRL  C            ;0 -> B7 ... B0 -> CY
        DJNZ V24OU7       ;naechstes Bit ausgeben
        POP  AF 
        POP  BC 
        POP  DE 
        POP  HL 
        RET 
;
;
; Funktion ":" --> Argument Anzeige (TODO)
;
        DEFB 00H,09H,3AH,0DH 
        CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC
        CALL L07F1
        RST  18H 
        DEFB 20H+80H 
        EX   DE,HL 
        CALL L07F1 
        LD   H,B 
        LD   L,C 
        RST  18H 
        DEFB 20H+80H 
        CALL L07F1 
        RST  18H 
        DEFB 0DH+80H 
        RET 
;
;
; Funktion "p" --> RAM loeschen
;
        DEFB 00H,09H,70H,0DH 
        LD   HL,1858H 
        LD   (HL),0FFH
        LD   DE,1859H 
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        LD   BC,0E7A7H    ;loeschen bis 0FFFFH
        ELSE              ;alle anderen Monitor-Versionen
        LD   BC,0E6A7H    ;loeschen bis 0FEFFH
        ENDIF
        LDIR 
        RST  18H 
        DEFM " Clear RAM" 
        DEFB 0DH+80H 
        RET
;
;
; Funktion "U" --> Port lesen (TODO)
;
        DEFB 00H,09H,55H,0DH;
        LD   HL,(ARG1)
        LD   C,L          ;Portnummer in C
        LD   DE,(ZWINL)   ;Bildschirm-Adresse
        INC  DE 
        CALL L07F7        ;CALL INHEX
FKTU1:  LD   (KURPOS),DE  ;Kursor positionieren
        RST  18H 
        DEFB 2AH+80H      ;"*+80H 
        IN   A,(C) 
        CALL OUTHEX       ;Wert ausgeben
FKTU2:  CALL L07FA        ;CALL TASTE
        JR   Z,FKTU2      ;warten, bis Taste gedrueckt
        CP   0D1H         ;"Q"?
        RET  Z            ;ja --> Ende
        JR   FKTU1 
;
;
; ??? (TODO)
;
L0EA5:  LDI 
        DEC  DE 
        DEC  DE 
        JP   PE,L0EA5 
        RET 
;
;
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
        DEFS 7,0FFH       ;Platzhalter 7 Byte
        ENDIF
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
;
;
; Funktion "X" --> AC1 ROM-Bank aufrufen (E000H bis FEFFH) - Teil 2
;
PROGX3: OUT  (0C1H),A     ;HWT EPROM + LED
        OUT  (0C0H),A     ;NWT EPROM
        JP   0E000H       ;Programmstart Prog-X
;
        ENDIF
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
;
; Funktion "8" --> PIO2-Karte: Bank 3 einschalten
;
        DEFB 00H,09H,38H,0DH 
        JP   FKT8 
;
        ENDIF
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
; Funktion "8" --> PIO2-Karte: Bank 3 einschalten
;
        DEFB 00H,09H,38H,0DH 
        JP   FKT8 
;
        ENDIF
;
;
; UP 'Joy', Abfrage Joystick 1
; ----------------------------
;
UPJOY:  PUSH BC           ;Adresse 0EB4H
        IN   A,(P05)      ;PIO1 Port B lesen
        LD   C,A 
        RES  1,A          ;Joystick aktivieren
        OUT  (P05),A      ;PIO1 Port B schreiben
        IN   A,(P04)      ;PIO1 Port A lesen
        LD   B,A 
        LD   A,C          ;Joystick deaktivieren
        OUT  (P05),A      ;PIO1 Port B schreiben
        LD   A,B          ;Joystick-Wert in A
        NOP 
        AND  1FH          ;nur 5 Bit + Z-Flag setzen
        POP  BC 
        RET
;
;
; Funktion "?" --> Help
;
        DEFB 00H,09H,3FH,0DH 
        RST  18H 
        DEFM "Help:" 
        DEFB 0DH+80H 
        LD   HL,0200H 
        LD   BC,0FE00H
L0ED9:  XOR  A 
        CPIR 
        RET  PO 
        LD   A,09H 
        CP   (HL) 
        JR   NZ,L0ED9 
        INC  HL 
        LD   D,(HL) 
        INC  HL 
        LD   A,0DH 
        CP   (HL) 
        JR   NZ,L0ED9 
        LD   A,D 
        RST  10H          ;Ausgabe des Kennbuchstabens
        RST  18H 
        DEFM "  " 
        DEFB 20H+80H 
        JR   L0ED9 
;
;
; weitere Bearb. fuer RST 10H   (Ausgabekanal, normal Bildschirm)
; ---------------------------
;
RST10A: LD   HL,V24REG    ;V.24-Kontrollregister
        BIT  7,(HL)       ;V.24-Ausgabe mit 8 Bit?
        JR   NZ,RST10B    ;ja
        AND  7FH          ;nein --> 7. Bit ruecksetzen
RST10B: INC  HL           ;HL --> I/O-Byte
        CP   18H          ;Ctrl-X schaltet zusaetzlich V.24 ein
        JR   Z,RST10C 
        CP   19H          ;Ctrl-Y schaltet Ein/Ausgabe auf Standard
        JR   NZ,RST10D 
        LD   (HL),11H 
        DEFB 21H          ;LD HL,0EECBH + nxt. Befehl ueberspringen!!!
RST10C: SET 5,(HL)        ;Code = 0CBH, 0EEH
        JR   RST10E 
;
RST10D: BIT  4,(HL) 
        CALL NZ,FKT33     ;Funktion auf Adresse 0033H - Ausgabe ueber Bildschirm
        BIT  5,(HL) 
        CALL NZ,V24OUT    ;V.24 Ausgabe
        BIT  6,(HL) 
        CALL NZ,0FFFFH 
        BIT  7,(HL) 
        CALL NZ,18F3H     ;Sprung zum UP User-Ausgabe
RST10E: POP  AF 
        POP  HL 
        RET 
;
;
; Funktion "=" --> Bildschirminhalt ablegen (TODO)
;
        DEFB 00H,09H,3DH,0DH 
        RST  18H 
        DEFB 0BH,06H+80H 
        LD   HL,(KURPOS) 
        RST  18H 
        DEFB 0EH 
        DEFM "0059" 
        DEFB 2FH+80H      ;"/+80H 
        RST  08H 
        RST  10H 
        LD   (KURPOS),HL 
        CP   38H 
        RET  NC 
        CP   30H 
        RET  C 
        SUB  30H 
        LD   B,A 
        INC  B 
        LD   DE,0800H 
        LD   HL,9FFFH 
L0F48:  ADD  HL,DE 
        DJNZ L0F48 
        EX   DE,HL 
        PUSH HL 
        POP  BC 
        LD   HL,1000H 
        JP   L0EA5
;
;
; Funktion "/" --> Bildschirmkopie zurueckschreiben (TODO)
;
        DEFB 00H,09H,2FH,0DH 
        LD   HL,9800H 
        LD   A,(ARG1) 
        CP   08H 
        RET  NC 
        INC  A 
        LD   DE,0800H 
        LD   B,A 
L0F66:  ADD  HL,DE 
        DJNZ L0F66 
L0F69:  LD   DE,17FFH 
L0F6C:  LD   BC,0800H 
        PUSH HL 
        CALL L0EA5 
        RST  18H 
        DEFB 01H,0BH,11H+80H 
        EX   DE,HL 
        POP  HL 
        CALL L07F1 
        RST  18H 
        DEFB 2DH+80H      ;"-+80H 
        EX   DE,HL 
        DEC  HL 
        CALL L07F1 
        INC  HL 
        RST  18H 
        DEFB 10H+80H 
        RST  08H 
        CP   0BH 
        JR   Z,L0F69 
        CP   0AH 
        RET  NZ 
        LD   BC,0F000H
        ADD  HL,BC 
        JR   L0F69 
;
        IFDEF MONI88      ;Monitor 10/88 - Originalversion 1988
;
;
; ??? (TODO)
;
L0F94:  JR   L0F6C
;
;
; ??? (TODO)
;
L0F96:  CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC
        EX   DE,HL
        INC  HL
        XOR  A
        SBC  HL,DE
        LD   BC,0FFFFH
L0FA1:  LD   A,(DE)
        XOR  B
        LD   B,A
        RRCA
        RRCA
        RRCA
        RRCA
        AND  0FH
        XOR  B
        LD   B,A
        RRCA
        RRCA
        RRCA
        PUSH AF
        AND  1FH
        XOR  C
        LD   C,A
        POP  AF
        PUSH AF
        RRCA
        AND  0F0H
        XOR  C
        LD   C,A
        POP  AF
        AND  0E0H
        XOR  B
        LD   B,C
        LD   C,A
        INC  DE
        DEC  HL
        LD   A,H
        OR   L
        JR   NZ,L0FA1
        PUSH BC
        RST  18H
        DEFM " CRC"
        DEFB 20H+80H
        POP  HL
        JP   OUTHL
;
        DEFS 8,0FFH       ;Platzhalter 8 Byte
;
; Funktion "r" --> Basic-Warmstart
;
        DEFB 00H,09H,72H,0DH 
        LD   A,02H
        OUT  (P14),A      ;Modul 1: EPROM mit Basic
        JP   5FD5H        ;Sprung ins Basic-Programm
;
;
; Funktion "X" --> Programmpaket X
;
        DEFB 00H,09H,58H,0DH 
        JP   JUMPX
;
;
; Funktion "b" --> Basic-Kaltstart
;
        DEFB 09H,62H,0DH  ;die 00H wird vom letzten Befehl genutzt
        LD   A,02H
        OUT  (P14),A      ;Modul 1: EPROM mit Basic
        JP   5FCCH        ;Sprung ins Basic-Programm
;
        ENDIF             ;Monitor 10/88 - Originalversion 1988
        IFDEF MONI88A     ;Monitor 10/88 - Originalversion mit Anpassungen
;
;
; ??? (TODO)
;
L0F94:  JR   L0F6C
;
;
; ??? (TODO)
;
L0F96:  CALL ARGLD        ;ARG1-->HL ARG2-->DE ARG3-->BC
        EX   DE,HL
        INC  HL
        XOR  A
        SBC  HL,DE
        LD   BC,0FFFFH
L0FA1:  LD   A,(DE)
        XOR  B
        LD   B,A
        RRCA
        RRCA
        RRCA
        RRCA
        AND  0FH
        XOR  B
        LD   B,A
        RRCA
        RRCA
        RRCA
        PUSH AF
        AND  1FH
        XOR  C
        LD   C,A
        POP  AF
        PUSH AF
        RRCA
        AND  0F0H
        XOR  C
        LD   C,A
        POP  AF
        AND  0E0H
        XOR  B
        LD   B,C
        LD   C,A
        INC  DE
        DEC  HL
        LD   A,H
        OR   L
        JR   NZ,L0FA1
        PUSH BC
        RST  18H
        DEFM " CRC"
        DEFB 20H+80H
        POP  HL
        JP   OUTHL
;
        DEFB 0FFH         ;Platzhalter 1 Byte
;
;
; Funktion "X" --> AC1 ROM-Bank aufrufen (E000H bis FEFFH) - Teil 1
;
        DEFB 00H,09H,58H,0DH 
PROGX1: LD   DE,8000H     ;Pointer auf EPROM + LED
        LD   HL,0E000H    ;Pointer auf Hauptspeicher
        LD   BC,1F00H     ;Laenge (E000H - FEFFH)
        XOR  A            ;XWT EPROM
        OUT  (0C6H),A
PROGX2: LD   A,D          ;HWT EPROM
        OUT  (0C1H),A
        LD   A,E          ;NWT EPROM
        OUT  (0C0H),A
        INC  DE
        IN   A,(0C2H)     ;Data
        LD   (HL),A
        INC  HL
        DEC  BC
        LD   A,B
        OR   C
        JR   NZ,PROGX2
        JP   PROGX3       ;zum Teil 2 der Funktion
;
        ENDIF             ;Monitor 10/88 - Originalversion mit Anpassungen
        IFDEF MONI11      ;Monitor 10/88 - BWS-CPLD-Version 2011
;
; Funktion "!" --> Farbbyte einstellen
;
        DEFB 00H,09H,21H,0DH
        LD   A,(ARG1) 
        OR   A            ;wenn ARG1=0 ist --> Vorgabewert nehmen
        JR   NZ,FINIT1 
;
;
; Farb-Initialisierung des CPLD
;
FINIT:  LD   A,02H        ;Farbbyte: nach Reset = 02H
FINIT1: PUSH AF 
        XOR  A            ;Wert fuer Monochrom-BWS
        LD   (COLOR),A    ;Merkzelle fuer Farbbyte im RAM schreiben
        POP  AF 
        LD   C,PF0        ;I/O-Adresse COLOR-BWS
        IN   B,(C)        ;Konfigbyte COLOR-BWS lesen
        INC  B            ;fuer COLOR-BWS ist B!=0
        RET  Z            ;kein COLOR-BWS vorhanden --> Ende
        DEC  B 
        LD   D,B 
        SET  2,D          ;Konfigbyte COLOR-BWS: Farb-RAM aktiv
        LD   HL,1000H 
        LD   E,(HL) 
        OUT  (C),D        ;COLOR-BWS auf Farb-RAM umschalten
        LD   (HL),A       ;Farbe auf 1000h setzen
        CP   (HL)         ;ist ein Farb-RAM eingebaut?
        OUT  (C),B        ;Konfigbyte COLOR-BWS: Text-RAM aktiv
        LD   (HL),E       ;Zeichen auf 1000h zurueckschreiben
        RET  NZ           ;Farbe hat nicht geklappt --> Ende
        OUT  (C),D        ;nochmal COLOR-BWS auf Farb-RAM umschalten
        PUSH BC 
        LD   DE,1001H 
        LD   BC,07FFH 
        LDIR              ;Farbe in den restlichen BWS-RAM schreiben
        LD   A,(HL)       ;Farbe aus 17FEh ruecklesen
        POP  BC 
        OUT  (C),B        ;Konfigbyte COLOR-BWS: Text-RAM aktiv
        LD   (COLOR),A    ;Merkzelle fuer Farbbyte im RAM schreiben
        RET 
;
;
; Funktion "7" --> PIO2-Karte: Bank 2 einschalten
;
        DEFB 00H,09H,37H,0DH 
        LD   A,20H        ;ehemals 10H, war falsch!!
        OUT  (P0F),A 
        JP   2000H        ;Programmstart von UserProg auf 2000H
;
;
; Funktion "r" --> Basic-Warmstart
;
        DEFB 00H,09H,72H,0DH 
        LD   A,02H
        OUT  (P14),A      ;Modul 1: EPROM mit Basic
        JP   5FD5H        ;Sprung ins Basic-Programm
;
;
; Funktion "X" --> Programmpaket X
;
        DEFB 00H,09H,58H,0DH 
        JP   JUMPX
;
;
; Funktion "b" --> Basic-Kaltstart
;
        DEFB 09H,62H,0DH  ;die 00H wird vom letzten Befehl genutzt
        LD   A,02H
        OUT  (P14),A      ;Modul 1: EPROM mit Basic
        JP   5FCCH        ;Sprung ins Basic-Programm
;
        ENDIF             ;Monitor 10/88 - BWS-CPLD-Version 2011
        IFDEF MONI11A     ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
; Funktion "!" --> Farbbyte einstellen
;
        DEFB 00H,09H,21H,0DH
        LD   A,(ARG1) 
        OR   A            ;wenn ARG1=0 ist --> Vorgabewert nehmen
        JR   NZ,FINIT1 
;
;
; Farb-Initialisierung des CPLD
;
FINIT:  LD   A,4FH        ;Farbbyte: nach Reset = 4FH
FINIT1: PUSH AF 
        XOR  A            ;Wert fuer Monochrom-BWS
        LD   (COLOR),A    ;Merkzelle fuer Farbbyte im RAM schreiben
        POP  AF 
        LD   C,PF0        ;I/O-Adresse COLOR-BWS
        IN   B,(C)        ;Konfigbyte COLOR-BWS lesen
        INC  B            ;fuer COLOR-BWS ist B!=0
        RET  Z            ;kein COLOR-BWS vorhanden --> Ende
        DEC  B 
        LD   D,B 
        SET  2,D          ;Konfigbyte COLOR-BWS: Farb-RAM aktiv
        LD   HL,1000H 
        LD   E,(HL) 
        OUT  (C),D        ;COLOR-BWS auf Farb-RAM umschalten
        LD   (HL),A       ;Farbe auf 1000h setzen
        CP   (HL)         ;ist ein Farb-RAM eingebaut?
        OUT  (C),B        ;Konfigbyte COLOR-BWS: Text-RAM aktiv
        LD   (HL),E       ;Zeichen auf 1000h zurueckschreiben
        RET  NZ           ;Farbe hat nicht geklappt --> Ende
        OUT  (C),D        ;nochmal COLOR-BWS auf Farb-RAM umschalten
        PUSH BC 
        LD   DE,1001H 
        LD   BC,07FFH 
        LDIR              ;Farbe in den restlichen BWS-RAM schreiben
        LD   A,(HL)       ;Farbe aus 17FEh ruecklesen
        POP  BC 
        OUT  (C),B        ;Konfigbyte COLOR-BWS: Text-RAM aktiv
        LD   (COLOR),A    ;Merkzelle fuer Farbbyte im RAM schreiben
        RET 
;
;
; Funktion "7" --> PIO2-Karte: Bank 2 einschalten
;
        DEFB 00H,09H,37H,0DH 
        LD   A,20H        ;ehemals 10H, war falsch!!
        OUT  (P0F),A 
        JP   2000H        ;Programmstart von UserProg auf 2000H
;
;
; Funktion "r" --> Basic-Warmstart
;
        DEFB 00H,09H,72H,0DH 
        LD   A,02H
        OUT  (P14),A      ;Modul 1: EPROM mit Basic
        JP   5FD5H        ;Sprung ins Basic-Programm
;
;
; Funktion "X" --> Programmpaket X
;
        DEFB 00H,09H,58H,0DH 
        JP   JUMPX
;
;
; Funktion "b" --> Basic-Kaltstart
;
        DEFB 09H,62H,0DH  ;die 00H wird vom letzten Befehl genutzt
        LD   A,02H
        OUT  (P14),A      ;Modul 1: EPROM mit Basic
        JP   5FCCH        ;Sprung ins Basic-Programm
;
        ENDIF             ;Monitor 10/88 - BWS-CPLD-Version mit Anpassungen
;
;
; Funktion "<SPACE>" --> nix tun...
;
        DEFB 00H,09H,20H,0DH 
        RET 
;
;
; Funktion "<ENTER>" --> nix tun...
;
        DEFB 00H,09H,0DH,0DH 
        RET
;
;
        .DEPHASE
        END
;
